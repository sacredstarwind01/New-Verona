-> Start

// Переменные
VAR Passion = 0 // Один из ключевых параметров пары - страсть. Путь страсти ведет к классическому трагичному финалу.
VAR Sense = 0 // Один из ключевых параметров пары - разум. Путь разума ведет к альтернативному финалу. В зависимости от количества дополнительной страсти пара может сбежать и остаться вместе или разойтись.
VAR Peace = 0  // Один из ключевых параметров пары - стремление к миру между семьями. Путь мира может привести к тому, что влюбленные останутся живы и помирят свои семьи.
VAR Lorenco = 0 // Показатель убежденности отца Лоренцо в готовности пары к браку.
VAR Tibalt = "alive" // Тибальт может умереть или отправиться в изгнание.
VAR Romeo = "alive" // Ромео может умереть или отправиться в изгнание, а также сбежать.
VAR Juliette = "alive" // Джульетта может умереть, а также сбежать и выйти замуж (обозначается именем мужа, например "Romeo" или "Paris").
VAR Exit = false // На старте игры Джульетта не согласилась на побег, но мы запомним, если она это сделает.
VAR Tell = false // На старте игры Джульетта не согласилась рассказать всем о тайном браке с Ромео, но мы запомним, если она это сделает.
VAR Parents = 0 // Показатель убежденности родителей, что нужно мириться.
VAR Paris = false // На старте игры Джульетта не согласилась на брак с Парисом, но мы запомним, если она это сделает.

=== Start ===
Всем нам знакома история Вероны — города, где давняя вражда двух семей, Монтекки и Капулетти, породила самую печальную историю на свете.
Но сегодня история Ромео и Джульетты в ваших руках. Вам решать, какие слова будут сказаны, какие поступки совершены и к каким последствиям это приведет.
Какой финал ждет героев вашей истории?
-> 1_Ball

=== 1_Ball ===
// Первая встреча Ромео и Джульетты на балу. Первое столкновение Ромео с Тибальтом.
Сцена 1. Бал-маскарад
Воздух ночной Вероны гудел от музыки - древнейший род Капулетти давал грандиозный бал-маскарад.
Ромео Монтекки знал, что ему не стоило приближаться к этому дому.
Но надежда увидеть среди гостей свою возлюбленную Розалину влекла его как магнит.

Ромео: Какая удача, что на бал-маскарад можно надеть маску и затеряться среди танцующих.
Ромео: Я проскользну в их дом незамеченным, найду ту, что пленила мое сердце, и исчезну до того, как кто-либо заподозрит, что в их доме побывал враг.

Меркуцио: Ну же, Ромео! Хватит вздыхать о той, что не смотрит на тебя!
Меркуцио: Первые красавицы Вероны соберутся на балу - открой глава и взгляни на них!
Меркуцио: Покажи всем, что Монтекки не боится ни врагов, ни любви!

Бенволио: Меркуцио, твои речи горячи, но безрассудны.
Бенволио: Мы идем в логово врага и должны сохранять холодную голову.

Ромео: Ах, милые друзья! У меня такое чувство, что беда уже на пороге, и ее не миновать. Я последую твоему совету...  #П

* [Меркуцио. Я найду свою любовь во что бы то ни стало!]
    ~ Passion = Passion + 1
     Страсть +1
* [Бенволио. Я буду осмотрителен и буду держать себя в руках.]
    ~ Sense = Sense + 1  
     Разум +1
- Ромео: Нам нужно спешить, если хотим попасть на бал.
Как только друзья вошли в зал, их закружила карусель пляшущих и смеющихся масок.
Взгляд Ромео рассеянно скользил по танцующим парам. 
И вдруг, сквозь всю эту пеструю суету, он увидел ЕЕ.

Джульетта: ...

Ромео: Как она сияет! Она затмила факелов лучи! #Р
Ромео: Неужто я любил? До этой минуты я и не знал, что такое любовь! #У
Ромео: Кто она? Как ее зовут? Я должен подойти к ней!

Джульетта: ... #Р
    // Заметила Ромео

Ромео: Простите мою дерзость, мне показалось, что весь этот зал на мгновение замер. #Р
Джульетта: Я бы сказала, что не замер, а просто… исчез. Остался лишь гул, как от далекого моря. #Р
Ромео: Для меня он превратился в неразборчивый шум, в котором лишь одна ясная нота — Ваша улыбка. #Р
Джульетта: А я чувствовала себя потерянной в этой череде лиц, пока Ваш взгляд не стал для меня маяком. #Р
Ромео: Значит, это мгновение мы украли у мира вместе. #Р
  // Картинка Ромео и Джульетты держатся за руки

Тибальт: Этот голос… Клянусь честью, это Ромео Монтекки! #У
Ромео: ...
Тибальт: Как ты посмел явиться сюда, чтобы глумиться над нашим праздником? #З
Тибальт: Как смеешь ты подходить к моей сестре Джульетте! #З

Ближайшие к ним гости зашеплтались, десятки глаз обратились в их сторону.
Тибальт, двоюродный брат Джульетты, с ненавистью смотрел на Ромео, его рука уже потянулась к шпаге. 
Ромео: Неужто Капулетти! Мой светлый ангел - дочь заклятого врага! Ах, вот какую беду я предвидел во сне... #У
Ромео: Как же мне быть? Тибальт настроен решительно, и я еще никогда не уходил от вызова. Но он брат той, кого я люблю, без кого я не хочу представлять свою жизнь. #У

Ромео решил:

// Критический выбор

// Бросить вызов Тибальту
* [Снять маску и встретить взгляд Тибальта]
    ~ Passion = Passion + 1
    Страсть +1
    Ромео выпрямил спину и открыл свое лицо. 
    По толпе пронесся испуганный вздох, когда стало ясно, что столкновение неизбежно. 
    Взбешенный Тибальт попытался вытащить шпагу, но Ромео успел толкнуть его, и в начавшейся суматохе опрокинул на пол серебряный поднос с кубками.
    Пока всеобщее внимание было приковано к беспорядку, Ромео воспользовался моментом и скрылся в толпе, бросив прощальный взгляд на Джульетту.
// Сбежать, чтобы не нарваться
* [Раствориться в толпе]
    ~ Sense = Sense + 1
    Разум +1
    Не говоря ни слова, Ромео опустил голову и сделал шаг назад, позволив толпе гостей скрыть его.
    Отступая он нежно коснулся руки Джульетты, и она обернулась, продолжая смотреть на него, пока не потеряла из виду.
    Тибальт сделал несколько резких шагов вперед, но поняв, что упустил его, издал разочарованный рык и бросился вон из зала.
// Не усугублять вражду между семьями
* [Обратиться к хозяину дома]
    ~ Sense = Sense + 1  
    ~ Peace = Peace + 1 
    Разум +1, Мир +1
    Ромео проигнорировал Тибальта и нашел глазами  главу семьи, старого Капулетти, громко обратился прямо к нему.
    Ромео: Милорд Капулетти, я благодарю вас за гостеприимство! Ваш дом прекрасен, как и те, кто в нем живет. 
    Тибальт не ожидал этого хода и замер, не смея напасть на гостя.
    Старый Капулетти с гордостью кивнул, бросив на племянника недовольный взгляд.
    Гости зашептались, а Ромео воспользовался замешательством, чтобы поцеловать руку Джульетте.
    Ромео: Я должен идти, чтобы не злить более вашего брата. Но знайте, мой ангел, вы всего дороже для меня!
    
- Когда Ромео скрылся, Джульетта подбежала к кормилице.
Джульетта: Кто этот юноша? Молю, узнай! #У
Кормилица: Дитя мое, его зовут Ромео, сын Монтеки.
Джульетта: Не может быть! Беду ждала я в том, что он уже женат. А он, оказывается, сын нашего врага... #П
Джульетта: Сколь разумнее было бы никогда его не встречать и не влюбляться. #П
Джульетта: Но разве я могу приказывать своему сердцу, когда оно так волнуется при мысли о нем? 
Кормилица: Вам не стоит видеться. Если он будет разумен, то не будет нарушать твой покой. А скоро родители выберут тебе достойного мужа.
Кормилица: Идем, Джульетта!
Позволяя кормилице увести себя, Джульетта продолжала думать о нем.
-> 2_Balcony

=== 2_Balcony ===

// Встреча Ромео и Джульетты под еее балконом. Признание в любви. Договоренность о свадьбе.
// Звезды на фоне

Сцена 2. У балкона
Покинув бал, Ромео шел сквозь ночную прохладу. Один образ горел в его сознании: ОНА. 
Он не мог представить, что покинет Джульетту, едва повстречав, и потому он шел к балкону Джульетты, надеясь продлить встречу.

Джульетта: Ромео, ах, зачем же ты Ромео?
Джульетта: Отрекись от отца и своего имени ! А если нет, то поклянись мне в любви, и я перестану быть Капулетти.
Ромео: Ловлю тебя на слове! Назови меня своей любовью, и с этого мига я больше не Ромео.
Джультта: Ах! Как ты сюда пробрался? Стены сада высоки и неприступны. Если мои родные найдут тебя здесь — тебе верная смерть! #У
Ромео: Любовь принесла меня на своих крыльях, и каменные стены ей не преграда. #Р
Джульетта: Но их ненависть найдет тебя... #П
Ромео: Тьма ночи скроет меня от их глаз. Лишь бы ты смотрела на меня с нежностью. #Р

 Ромео подошел ближе к балкону, его глаза сияли в лунном свете. Джульетта чувствовала, как ее сердце стучало в груди. 
 Союз с Ромео был опасен, но в это мгновение ОН - все, о чем она хотела думать. Она решила:

// Критический выбор
* [Довериться сердцу и признаться в любви]
    ~ Passion = Passion + 1
    Страсть +1
    Джульетта: К чему сомнения, если я уже знаю ответ в своем сердце? 
    Джульетта: Любовь в тебе как вспышка молнии, которая озарила мою жизнь. Ромео, любишь ли ты меня?
    Ромео: Душа моя, моя любовь к тебе - единственный свет в моей жизни. До встречи с тобой я блуждал во тьме. 
    Ромео: Ты — само солнце, вокруг которого теперь вращается моя вселенная, и без тепла которого я мгновенно погибну.
    Ромео взбирается на балкон и припадает к рукам Джульетты.
    Джульетта: Возьми мою любовь в обмен на твою. Я отдаю тебе свое сердце, свою судьбу, все, что у меня есть!
    // Картинка поцелуя

* [Проверить серьезность намерений Ромео]
    ~ Sense = Sense + 1
    Разум +1
    Джульетта: Этот союз так внезапен, так похож на вспышку молнии, что гаснет, не успев осветить тьму.
    Джульетта: Я боюсь, что наша радость так же быстро исчезнет.
    Ромео: Клянусь священным ликом луны, что серебрит верхушки этих деревьев...
    Джульетта: О, не клянись луной, изменчивой луной! Твоя любовь может оказаться такой же непостоянной.
    Джульетта: Ромео, если твои намерения честны, а цель твоя — брак, то завтра я пришлю к тебе своего вестника.
    Джульетта: Передай с ним, где и когда ты хочешь совершить обряд.
    Джульетта: И тогда я положу свою судьбу к твоим ногам и пойду за тобой на край света.
    // Картинка Ромео под балконом
    
* [Поделиться с Ромео переживаниями об их семьях]
    ~ Sense = Sense + 1  
    ~ Peace = Peace + 1 
    Разум +1, Мир +1
    Джульетта: Ромео, наша любовь — хрупкий цветок, выросший на каменистой почве ненависти. Мы не сможем быть счастливы, пока наши семьи ненавидят друг друга.
    Ромео: Ты права, мой ангел! От этого она еще более драгоценна. Мы обязательно найдем способ все изменить!
    Джульетта: Боюсь, нам не справиться вдвоем. Нам нужен проводник и мудрый советник, который проведет и укажет дорогу.
    Ромео: Отец Лоренцо! Он свят и видит дальше, чем мы. Встретимся у него завтра!
    Джульетта: Да, Ромео! Иди к нему на рассвете. А я, как только будет возможно, пришлю к тебе верного человека.
    
- Ромео: Завтра же я договорюсь об обряде венчания! Я не хочу более и дня жить без тебя!
Джульетта: Тогда лети! Скорее бы прошла ночь, приближая этот сладостный день!
    
-> 3_Wedding

=== 3_Wedding ===
// Влюбленные убеждают отца Лоренцо тайно поженить их, а он проводит им проверку. 
Сцена 3. В келье
Монах стоял у икон и молился. Свет Луны пробивался через узкое окно кельи, освещая пучки сушеных трав и старинные книги. 
Ромео: Джульетта!
Джульетта: Ромео!

Отец Лоренцо: Дети мои. Вы пришли ко мне, ища таинства, которое свяжет вас навек. 
Отец Лоренцо: Но любовь, рожденная в спешке, часто приводит к горькому финалу. 
Отец Лоренцо: Я служу Господу, а не слепой страсти. Прежде чем я соглашусь стать соучастником этого рискованного союза, вы должны ответить на мои вопросы.
Отец Лоренцо: Отвечайте честно, ибо я смотрю не на ваши лица, а в ваши души.
-> passion_test

== passion_test ==
Отец Лоренцо: Вопрос первый тебе, Джульетта. О вашей любви.
Отец Лоренцо: Любовь, рожденная во взгляде, хрупка. Что доказывает, что ваша связь — не просто мимолетный каприз, а сила, способная сокрушить стены, что воздвигли ваши семьи?
Джульетта: ...
* [Наша любовь дает нам силы действовать!]
    ~ Passion = Passion + 1
    ~ Lorenco = Lorenco + 1
    Страсть +1
    Джульетта: Любовь уже заставила его рискнуть жизнью, а меня — пойти против воли отца.
    Джульетта: Мы выбираем друг друга вопреки всему и готовы всем пожертвовать друг для друга.
* [Он занимает все мои мысли!]
    Джульетта: Я чувствую любовь всем сердцем! Когда я с ним, ничего другого не существует. Наша страсть сожжет любые преграды!
* [Мы просто хотим быть вместе!]
    Джульетта: Разве этого не может быть достаточно? Мы никому не желаем зла.
 
- Лоренцо выслушал Джульетту, не изменившись в лице, и задал следующий вопрос Ромео.
   -> sense_test

== sense_test ==
Отец Лоренцо: Второй вопрос о вашем будущем. Пламя страсти согревает, но со временем оно утихает, оставляя угли. 
Отец Лоренцо: Жизнь — это не только прогулки под луной, но и совместные будни, полные забот. Что вы будете делать, когда восторг пройдет и останется лишь привычка?
Ромео: ...
* [Когда восторг пройдет, начнется настоящая работа.]
    ~ Sense = Sense + 1
    ~ Lorenco = Lorenco + 1
    Разум +1
    Ромео: Мы будем учиться быть друзьями, доверять друг другу, прощать слабости. 
    Ромео: Угли, о которых вы говорите, отец, могут согревать гораздо дольше, чем пламя. 
    Ромео: Наша цель - построить очаг, а не разжечь костер.
* [Наша любовь не может угаснуть, святой отец!]
    Ромео: Это пламя, что касается небес! Привычка — это удел других, а не нас. 
    Ромео: Мы будем смотреть друг на друга с тем же восторгом до последнего вздоха!
* [Если огонь нашей любви начнет угасать, я сделаю все, чтобы он разгорелся вновь!]
    Ромео: Лучше сгореть дотла вместе, чем жить в сером пепле привычки.
    
- Лоренцо выслушал Ромео, по-прежнему не меняя выражения лица.
-> peace_test

== peace_test ==
Отец Лоренцо: Вопрос третий. О вашей цели. Этот брак в глазах Господа может стать лекарством для ран Вероны. 
Отец Лоренцо: Но в глазах людей он — искра в пороховой бочке. Как вы объявите семьям о вашем браке?

* [Нам нет дела до их войны.]
    Ромео: Мой дом теперь - это Джульетта. Мы будем вместе, куда бы судьба нас ни завела.
    Джульетта: Даже если нам придется покинуть наши семьи, я готова прожить свою жизнь с Ромео.
* [Как только они увидят нашу любовь, они помирятся!]
    Джульетта: Их сердца растают, и они не смогут больше враждовать!
    Ромое: Я не подниму меча против семьи Джульетты и, уверен, они поступят соответственно.
* [Мы готовы стать первой нитью, сшивающей рану между нашими домами.]
    ~ Peace = Peace + 1 
    ~ Lorenco = Lorenco + 1 
    Мир +1    
    Ромео: Вражда длится так давно, что никто уже и не помнит, с чего она началась. Настало время ее остановить.
    Джульетта: Нам придется проявить терпение и смелость. Но мы готовы попытаться, ведь мы являемся продолжением наших семей.
    
- Отец Лоренцо кивнул и приготовился дать ответ.
-> final_test

== final_test ==
// Поженятся ли герои
{ Lorenco == 0:
    // Лоренцо не убежден и отказывается заключать брак.
    - Отец Лоренцо: Простите, дети мои, но я не стану орудием вашей погибели. 
      Отец Лоренцо:  Ваши ответы показали мне лишь слепую страсть и детские мечты. Ваш союз не принесет мира, он разожжет еще больший пожар. 
      Отец Лоренцо: Вы не готовы. Возвращайтесь в свои дома и молитесь о разуме, а не о любви. В браке вам отказано.
      Джульетта: Я боялась, что вы скажете именно так, святой отец... #П
      Ромео: Не бойся! Я по-прежнему верен своему слову!
      Ромео: Наша любовь найдет способ прорасти, и мы соединим себя узами брака, когда будем готовы.
      Ромео: Мы должен идти, но время разлуки я буду мерить не ходом Солнца, а мыслями о тебе. И знай, что этих мыслей будет так много, что они приблизят нашу следующую встречу.
    -> final_test_end
- else:
    { Lorenco == 3:
        // Лоренцо убежден и заключает брак.
        - Страсть +1, Разум +1, Мир +1
        Отец Лоренцо: Сам Господь говорит вашими устами. В ваших ответах я вижу не только страсть, но и мудрость, и смирение.
        Отец Лоренцо: Этот союз действительно благословлен. Пойдемте, дети мои. Я обвенчаю вас немедленно.
        ~ Juliette = "Romeo"
        ~ Sense = Sense + 1
        ~ Passion = Passion + 1
        ~ Peace = Peace + 1
        -> wedding
    - else:
        // Лоренцо сомневается и уточянет, намерены ли герои заключить брак.
        Отец Лоренцо: В ваших словах есть и правда, и опасная наивность. Ваше желание искренне, но фундамент вашего будущего шаток. 
        Отец Лоренцо: Я вижу любовь, но не вижу полной готовности к последствиям.
        Отец Лоренцо: Скажите мне честно, осознавая все риски, вы по-прежнему просите меня обвенчать вас?
        
        * [Заключить брак]
            ~ Juliette = "Romeo"
            ~ Sense = Sense + 1
            ~ Passion = Passion + 1
            Разум +1
            Страсть +1
            Ромео: Да, мы готовы ко всему!
            Джульетта: Лишь ему я готова вручить свою судьбу! #Р
            Отец Лоренцо: Да будет так. И да поможет нам всем Господь.
            -> wedding
        * [Отложить брак]
            Джульетта: Вы правы, отец Лоренцо, нам нужно еще подумать.
            Ромео: Наша любовь найдет способ прорасти, и мы соединим себя узами брака, когда будем готовы.
            Ромео: Мы должны идти, но время разлуки я буду мерить не ходом Солнца, а мыслями о тебе. И знай, что этих мыслей будет так много, что они приблизят нашу следующую встречу.
            -> final_test_end
    }    
}

== wedding ==
// Картинка свадьбы
Клятвы Ромео и Джульетты были не громкими речами для гостей, а тихим шепотом, предназначенным только друг для друга и для Бога. 
Он взял ее руку, холодную от волнения, и почувствовал, как ее пальцы крепко сжали его в ответ. 
Отец Лоренцо произнес слова, которые превратили двух врагов по рождению в единое целое по воле небес. 
-> final_test_end


== final_test_end ==
// В итоге влюбленные расходятся по домам.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}


-> 4_Mercucio_death

=== 4_Mercucio_death ===
// Ромео возвращается от отца Лоренцо и видит своих друзей Меркуцио и Бенволио, которые встретилсь с Тибальтом. Конфликт переходит в кровавую фазу.
Сцена 4. Роковая встреча

Ромео шел по улицам Вероны, и его сердце наполняла тайная радость, которая делала весь мир новым и ярким.
Но на раскаленной полуденной площади он увидел своих друзей, Меркуцио и Бенволио, которые столкнулись с Тибальтом.

Меркуцио: Ромео? Вы ищете синьора Ромео? 
Меркуцио: Боюсь, его расписание на сегодня уже заполнено. Но я могу записать вас в лист ожидания. Правда, он довольно длинный, и я слышал, синьор Ромео не принимает тех, кто дурно пахнет злобой.
Бенволио: Меркуцио, прошу тебя, уйдем. В такой день кровь кипит сама по себе, и драки не миновать.

Тибальт: ...
Тибальт: Монтекки! Наконец-то я нашел тебя. Твои друзья не выдали тебя, но ты явился сам. #У
Тибальт: Ты — подлец, что посмел явиться в мой дом прошлой ночью. Ты должен поплатиться за это, доставай свою шпагу. #З 

Ромео: Тибальт, есть причина, по которой я должен любить тебя. Она озволяет мне простить твою ярость и  оскорбление.
Ромео: Я не подлец. А потому прощай, я вижу, ты не знаешь, кто я.

Меркуцио: Что за позорное, трусливое смирение! #У
Меркуцио: Честь требует отмщения! Тибальт, крысиный царь, дерись со мной! #З

Тибальт: Ну что ж, Меркуцио. Раз ты так рвешься в бой, я к твоим услугам.

Шпаги со звоном скрестились. 
Ромео, в ужасе, бросился между ними.
Пытаясь их разнять, Ромео встал так, что на мгновение закрыл Меркуцио обзор.
Этого хватило, чтобы Тибальт нанес смертельный удар под рукой Ромео.

// Слова Меркуцио перед смертью
Меркуцио: Я сделал это ради тебя, понимаешь? 
Меркуцио: Я не мог просто стоять и слушать, как он оскорбляет тебя, а ты молчишь. Кто-то должен был защитить твою честь, раз уж ты сам не стал. 
Меркуцио: Но теперь... теперь из-за этой глупой, бесконечной вражды я умираю. 
Меркуцио: Чума... чума на оба ваших дома!

Меркуцио упал на руки Бенволио и затих навсегда.
Счастье, переполнявшее Ромео всего минуту назад, сменилось ледяным горем и обжигающей яростью.
Кровь его лучшего друга взывала к отмщению. Но клятва, данная Джульетте, шептала о мире.

// Критический выбор
Ромео решил:
// Классический путь, ведущий к изгнанию.
* [Отомстить немедленно, убив Тибальта] 
    ~ Tibalt = "Dead"
    ~ Romeo = "Exit"
    ~ Passion = Passion + 1 
    Страсть +1
    Ярость затмила разум Ромео. Он вытащил шпагу.
    Бой был коротким и жестоким, движимый горем, Ромео оказался быстрее.
    Тибальт упал замертво к его ногам.
    Ромео замер, глядя на то, что наделал. Шпага выпала из его ослабевших пальцев. Огонь ярости, что кипел в нем, погас, оставив после себя лишь холодный, черный ужас. 
    Не просто враг... это двоюродный брат Джульетты. Ее кровь.
    Он отомстил за Меркуцио, но какой ценой? Одним ударом он уничтожил их будущее. 
    Как она теперь посмотрит на него? Он — убийца ее брата.
    Ромео: О, я игрушка судьбы!
    Бенволио: Беги, Ромео! Герцог казнит тебя!
    Ноги Ромео словно вросли в раскаленные камни площади. Он смотрел на два тела у своих ног — друга и врага — и не мог сдвинуться с места.
    Бенволио: Ромео, очнись! Слышишь? Городская стража уже близко! Если они найдут тебя здесь, все будет кончено!
    Слово "кончено" вырвало Ромео из оцепенения. Он бросил последний, полный отчаяния взгляд на лицо Меркуцио, затем развернулся и побежал. 
    Он бежал, не разбирая дороги, унося с собой не торжество мести, а лишь ледяной ужас и осознание того, что одним ударом клинка он разрушил свой мир и, возможно, потерял Джульетту навсегда.
    -> 5_1_Farewell
    
// Альтернативные пути, доступные по статам.
* {Juliette == "Romeo" and Sense >=3} [Сдержать гнев и потребовать правосудия у Герцога.]
    ~ Tibalt = "Exit"
    ~ Sense = Sense + 1 
    Ромео опустил руку, не давая ярости взять верх.
    Ромео: Я не стану убийцей. Но он ответит за это!
    Когда прибыла городская стража, Ромео указал на убийцу.
    Ромео: Вот он! Тибальт из дома Капулетти убил Меркуцио, родича Герцога! Я требую правосудия!
    Стражники, увидев герб Меркуцио, без колебаний окружили Тибальта.
    Реакция Тибальта на арест:
    Тибальт с яростью и презрением посмотрел на стражников, а затем на Ромео.
    Тибальт: Уберите от меня руки! Ты, трус, Монтекки! Прячешься за законом, вместо того чтобы драться как мужчина! Ты за это ответишь!
    В этот момент на площадь прибыл сам Герцог Вероны, его лицо было мрачнее тучи. Он увидел тело своего родственника, Меркуцио, и его голос загремел над площадью.
    Герцог: Моя собственная кровь пролита на улицах Вероны из-за вашей бессмысленной вражды! 
    Герцог: Тибальт, за убийство Меркуцио я должен был бы приговорить тебя к смерти. 
    Герцог: Но поскольку Ромео, друг убитого, не стал вершить самосуд, а воззвал к закону, я тоже проявлю милосердие. 
    Герцог: Я изгоняю тебя из Вероны! Если ты когда-либо будешь замечен в стенах этого города, в тот же час ты умрешь!
    Лицо Тибальта исказилось от ярости. Для такого гордого дворянина изгнание было хуже смерти. 
    Он боролся в руках стражи, его глаза горели ненавистью, устремленной на Ромео, пока стража силой уводила его с площади. 
    Ромео смотрел, как уводят Тибальта, и не чувствовал радости. 
    Да, он был жив и свободен. Меркуцио был отомщен. Но какой ценой? 
    Он только что стал причиной изгнания двоюродного брата Джульетты. Как она посмотрит на него теперь? 
    Он избежал гибели на площади, но, возможно, только что навсегда погубил их любовь.
    -> 5_2_Date
    
* {Juliette == "Romeo" and Peace >=3} [Сдержать гнев и потребовать у семей перестать враждовать.]
    ~ Parents = Parents + 1
    ~ Peace = Peace + 1 
    Мир +1
    По краю площади стали собираться зеваки. Кто с ужасом, кто с ненавистью, кто со слезами смотрели на разыгравшуюся драму.
    Ромео проигнорировал торжествующего Тибальта. Встав над телом Меркуцио, он обратился к толпе, где стояли и Монтекки, и Капулетти.
    Ромео: Он кричал: "Чума на оба ваших дома!", и он был прав!
    Ромео: Посмотрите, что наделала наша общая ненависть! Сколько еще должно умереть за гордыню наших отцов?
    Тибальт: Это ты! Это все из-за тебя, Монтекки! 
    Тибальт: Если бы ты ответил на мой вызов как мужчина, он был бы сейчас жив! 
    Тибальт: Твоя трусость, твои лживые речи о мире — вот что заставило его обнажить шпагу!
    Ромео: Ты ищешь виноватого, Тибальт, и я не стану тебя за это судить. 
    Ромео: Но моя честь не в том, чтобы проливать кровь, а в том, чтобы остановить ее. 
    Ромео: Не моя трусость убила его, и не твой клинок. Его убила наша ненависть. 
    Ромео: Она вложила шпагу в твою руку и запечатала мой рот. Мы можем и дальше бросаться обвинениями, пока не останется никого, кто мог бы их услышать. 
    Ромео: Или мы можем остановиться. Здесь. Сейчас.
    В толпе воцарилось потрясенное молчание. Семена мира были посеяны ценой жизни Меркуцио.
    Бенволио: Городская стража уже близко! 
    Тибальт посмотрел на безжизненное тело Меркуцио, затем снова на Ромео, и в его взгляде промелькнуло отчаяние.
    Развернувшись, он скрылся в лабиринте веронских улиц.
    -> 5_2_Date

=== 5_1_Farewell ===
// Ромео изгнан
Сцена 5. На закате
Закат окрасил небо Вероны в тревожные, кровавые тона. 
Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. 

{ Juliette == "Romeo":
    - Он взобрался наверх.
    Джульетта: О, Ромео! Это правда? Тибальт... он мертв от твоей руки? 
    Джульетта: Как мог мой дорогой муж стать убийцей моего брата?
    Ромео: Он убил Меркуцио, Джульетта. Моего лучшего друга. 
    Ромео: Я не владел собой, я был ослеплен горем! Я бы отдал все, чтобы этого не случилось.
    Джульетта: Мое сердце разбито на две части. Одна оплакивает брата, другая — страдает за мужа. 
    Джульетта: Но ты — мой муж. И моя верность — с тобой, до самого конца.
    -> early_bird
- else:
    Джульетта: Как ты мог? 
    Джульетта: Тибальт... он был моей семьей! 
    Джульетта: Я смотрела на тебя и видела ангела, но неужели за этим лицом скрывался тот, кто способен на убийство?
    Ромео: Он убил Меркуцио, Джульетта. Моего лучшего друга. 
    Ромео: Я не владел собой, я был ослеплен горем! Я бы отдал все, чтобы этого не случилось.
    Джульетта: Я должна ненавидеть тебя... Весь мой род требует этого. 
    Джульетта: Но мое глупое сердце... оно не может. Оно все еще любит тебя.
    -> early_bird
}

== early_bird ==
Гнев и обида утихли, оставив после себя лишь огромную, общую на двоих печаль. 
Они знали, что это их последняя встреча. На рассвете Ромео должен был навсегда покинуть Верону, спасаясь от гнева Герцога. 
Каждая секунда вместе была украденным у судьбы сокровищем, и оба понимали, что оно утекает сквозь пальцы.

// Критический выбор
Джульетта умоляла Ромео:
* [Остаться еще немного, чтобы продлить мгновение]
    ~ Passion = Passion + 1
    Страсть +1
    Джульетта: Нет... не уходи. Останься еще на час, на мгновение! Я не готова прощаться!
    Ромео: Любовь моя, каждый миг здесь приближает ко мне смерть. Но ради еще одной минуты с тобой я готов рискнуть вечностью.
    Джульетта: Нет, Ромео! Не рискуй ради меня! Моя любовь к тебе сильнее желания удержать тебя здесь.
* [Бежать немедленно ради его безопасности]
    ~ Sense = Sense + 1
    Разум +1
    Джульетта: Ты должен уйти. Сейчас же. 
    Джульетта: Моя любовь к тебе сильнее желания удержать тебя здесь. Я лучше буду жить без тебя, зная, что ты в безопасности, чем потеряю тебя навсегда из-за своей слабости. 
    Джульетта: Беги, Ромео!
    
- Ромео: Прощай, любовь моя. Живи и верь в нашу скорую встречу.
Он поцеловал ее в последний раз — поцелуй был соленым от слез — и спрыгнул с балкона. 
Он исчез в утреннем тумане, оставив Джульетту одну, с разбитым сердцем и ужасающей пустотой впереди.

Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}

-> END

=== 5_2_Date ===
// Ромео НЕ изгнан. Вместо прощания — тайная встреча. 
Сцена 5. На закате
Закат окрасил небо Вероны в тревожные, кровавые тона. 

{ Tibalt == "Exit":
    // Тибальт изгнан Герцогом
    - Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. Он взобрался наверх.
      Джульетта: Ромео! Ты жив, ты здесь! Я так боялась...
      Джульетта: Но... это правда? Тибальт... мой двоюродный брат... он изгнан из-за тебя?
      Ромео: Я не хотел этого, любовь моя, клянусь. 
      Ромео: Но он убил Меркуцио. Я выбрал закон, а не месть, чтобы не запятнать нашу любовь его кровью.
    -> we_choose
- else:
    // Тибальт ушел с площади
    Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. Он взобрался наверх.
    Джульетта: Они говорят, ты стоял над телом Меркуцио и говорил о мире... 
    Джульетта: Ты был так смел, так благороден! Но Тибальт... 
    Джульетта: Я видела его глаза, когда он заходил сегодня. Он этого не простит. Теперь его ненависть будет личной.
    Ромео: Я знаю. Я видел это в его взгляде. Поэтому я здесь.
    -> we_choose
}

== we_choose ==
Ромео: Ненависть повсюду. На улицах, в наших домах.
Ромео: Единственный островок мира во всей Вероне — это мы с тобой, здесь, сейчас.
Джульетта: Но как долго мы сможем скрываться? Ждать, пока Тибальт вернется, чтобы отомстить? 
Джульетта: Или пока наши отцы решат нашу судьбу за нас? Что нам делать, Ромео?

Его взгляд был полон решимости, но и он не знал ответа.
Будущее было чистым листом, и им нужно было решить, что на нем написать.

// Критический выбор
* {Passion >= 4} [Решиться на побег]
    ~ Exit = true
    ~ Passion = Passion + 1
    Страсть +1
    Ромео: Тогда давай покинем этот мир ненависти! Бежим из Вероны сегодня же ночью! 
    Ромео: Мантуя... любой другой город, где наши имена ничего не значат. Главное — вместе.
    Джульетта: Бежать... Да. Да! Какая разница, где жить, если я буду жить с тобой?
    
* {Juliette == "Romeo"} [Решиться объявить о бракосочетании]
    ~ Tell = true
    ~ Passion = Passion + 1
    ~ Sense = Sense + 1
    ~ Peace = Peace + 1
    Разум +1
    Страсть +1
    Мир +1
    Джульетта: Наша любовь чиста, и наш брак благословлен Богом.
    Ромео: Что ты предлагаешь?
    Джульетта: Завтра же мы расскажем все нашим родителям. Они не смогут разорвать союз, скрепленный на небесах.
    
* [Ничего не предпринимать]
    Джульетта: Я не знаю, Ромео... Все произошло так быстро. Моя голова идет кругом. 
    Джульетта: Давай подождем. Просто один день.
    Ромео: Один день может стоить нам всего, любовь моя.
    Джульетта: Я знаю. Но принять решение в таком смятении — еще опаснее. Дай мне время до завтра.

- Тьма окончательно сгустилась над Вероной.
Ромео: Мне пора уходить. Оставаться здесь — безумие.
Джульетта: Будь осторожен, любимый. Теперь каждый уголок этого города — ловушка для тебя.
Он поцеловал ее руки и скрылся в ночи, оставив Джульетту наедине с их общим, таким непростым решением.

Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
-> END
