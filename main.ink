-> Start

// Переменные
VAR Passion = 0 // Один из ключевых параметров пары - страсть. Путь страсти ведет к классическому трагичному финалу.
VAR Sense = 0 // Один из ключевых параметров пары - разум. Путь разума ведет к альтернативному финалу. В зависимости от очков страсти по пути разума пара может сбежать и остаться вместе (чтобы не впасть в отчаяние и организовать побег, нужно еще немного разума) или расстаться.
VAR Peace = 0  // Один из ключевых параметров пары - стремление к миру между семьями. Путь мира может привести к тому, что влюбленные останутся живы и помирят свои семьи.
VAR Lorenco = 0 // Показатель убежденности отца Лоренцо в готовность пары к браку.
VAR Tibalt = "ok" // Тибальт может умереть или отправиться в изгнание.
VAR Romeo = "ok" // Ромео может умереть или отправиться в изгнание, а также сбежать.
VAR Juliette = "ok" // Джульетта может умереть, а также сбежать или выйти замуж (обозначается именем мужа, например "Romeo" или "Paris").
VAR Exit = false // На старте игры Джульетта не согласилась на побег, но мы запомним, если она это сделает.
VAR Tell = false // На старте игры Джульетта не согласилась рассказать всем о тайном браке с Ромео, но мы запомним, если она это сделает.
VAR Parents = 0 // Показатель убежденности родителей, что нужно мириться.

=== Start ===
#Location: Start
Всем нам знакома история Вероны — города, где давняя вражда двух равноуважаемых семей, Монтекки и Капулетти, породила самую печальную историю на свете.
Но сегодня судьба Ромео и Джульетты в ваших руках. Вам решать, какие слова будут сказаны, какие поступки совершены и к каким последствиям это приведет.
Какой финал ждет героев вашей истории?
-> 1_Ball

=== 1_Ball ===
// Первая встреча Ромео и Джульетты на балу. Первое столкновение Ромео с Тибальтом.
#Location: Verona
Сцена 1. Бал-маскарад
Воздух ночной Вероны гудел от музыки - древнейший род Капулетти давал грандиозный бал-маскарад.
Ромео Монтекки знал, что ему не стоило приближаться к дому тех, с кем его семья враждовала многие годы. Но надежда увидеть среди гостей свою возлюбленную Розалину влекла его как магнит.

Ромео: Какая удача, что на бал-маскарад можно надеть маску и затеряться среди танцующих.
Ромео: Я проскользну в их дом незамеченным, найду ту, что пленила мое сердце, и исчезну до того, как кто-либо заподозрит, что в их доме побывал враг.

Меркуцио: Ну же, Ромео! Хватит вздыхать о той, что не смотрит на тебя!
Меркуцио: Первые красавицы Вероны соберутся на балу - открой глава и взгляни на них! Покажи всем, что Монтекки не боится ни врагов, ни любви!

Бенволио: Меркуцио, твои речи горячи, но безрассудны.
Бенволио: Мы идем в логово врага и должны сохранять холодную голову.

(В этой игре вам предстоит делать выборы. Некоторые выборы прокачивает показатели, от которых зависит, какие выборы будут вам доступны и как повернется сюжет.)
(Будьте внимательны, совершая тот или иной выбор!)

Ромео: Ах, милые друзья! Как хорошо, что вы оба со мной! С вами мне ничего не страшно!
Ромео: Я последую совету...  
+ [Меркуцио. Я найду свою любовь во что бы то ни стало!]
    ~ Passion = Passion + 1
     Страсть +1
+ [Бенволио. Я буду осмотрителен и буду держать себя в руках.]
    ~ Sense = Sense + 1  
     Разум +1
- Ромео: Пойдемте, друзья.

#Location: Ball
Как только юноши вошли в зал, их закружила карусель пляшущих и смеющихся гостей. Музыка гремела под высокими сводами, и в ритме бранля, держась за руки, юноши и девушки образовывали живую, струящуюся реку из шелка и бархата.
Взгляд Ромео рассеянно скользил по лицам, тщетно пытаясь найти в этом вихре Розалину, причину своей печали.
И вдруг, сквозь всю эту пеструю суету, он увидел ЕЕ.

Джульетта: ...

Она была в самом сердце танца, но казалась отделенной от всех. Она была так юна, что в ее движениях не было заученной, холодной грации других придворных дам, а была лишь чистая, естественная радость. 
Когда она смеялась, ее глаза сияли ярче, чем драгоценности в ее волосах. Свет от сотен свечей, казалось, игнорировал всех остальных и собирался только на ней, заставляя ее синее платье мерцать, словно ночное небо.

Ромео: Как она сияет! Она затмила факелов лучи! #Р
Ромео: Неужто я любил? До этой минуты я и не знал, что такое любовь! #У
Ромео: Кто она? Как ее зовут? Я должен подойти к ней! #У

Джульетта почувствовала на себе чей-то взгляд. Незнакомец в маске, стоявший в тени колонны, смотрел на нее с таким изумлением и благоговением, словно увидел чудо.
Взгляд не оценивал, не требовал, а лучился счастьем. Джульетта широко улыбнулась и потянулась мысленно к этому юноше, который продвигался к ней через весь зал.
Джульетта: ... #Р
    // Заметила Ромео

#Location: Romeo_and_Juliette_Ball
Ромео: Простите мою дерзость, мне показалось, что весь этот зал на мгновение замер. #Р
Джульетта: Я бы сказала, не замер, а просто… исчез. Остался лишь гул, как от далекого моря. #Р
Ромео: Для меня он превратился в неразборчивый шум, в котором лишь одна ясная нота — ваша улыбка. #Р
Джульетта: А я чувствовала себя потерянной в этой череде лиц, пока ваш взгляд не стал для меня маяком. #Р
Ромео: Значит, это мгновение мы украли у мира вместе. #Р
  // Картинка Ромео и Джульетты держатся за руки

#Location: Ball
Тибальт: Этот голос… Клянусь честью, это Ромео Монтекки! #У
Ромео: ...
Тибальт: Как ты посмел явиться сюда, чтобы глумиться над нашим праздником? #З
Тибальт: Как смеешь ты подходить к моей кузине Джульетте! #З

Ближайшие к ним гости зашептались, десятки глаз обратились в их сторону.
Тибальт, двоюродный брат Джульетты, с ненавистью смотрел на Ромео, его рука уже легла на эфес шпаги. 
Ромео: Неужто Капулетти! Мой светлый ангел - дочь заклятого врага! Ах, вот какую беду я предвидел во сне... #У
Ромео: Как же мне быть? Тибальт настроен решительно, и я еще никогда не уходил от вызова. Но он брат той, кого я люблю. #У

// Критический выбор

Ромео: Я должен...
// Бросить вызов Тибальту
+ [снять маску и встретить взгляд Тибальта]
    ~ Passion = Passion + 1
    Страсть +1
    Ромео выпрямил спину и открыл свое лицо. 
    По толпе пронесся испуганный вздох, когда стало ясно, что столкновения не избежать. 
    Взбешенный Тибальт попытался вытащить шпагу, но Ромео успел толкнуть его, и в начавшейся суматохе опрокинул на пол серебряный поднос с кубками.
    Пока всеобщее внимание было приковано к беспорядку, Ромео воспользовался моментом и скрылся в толпе, бросив прощальный взгляд на Джульетту.
    
// Сбежать, чтобы не нарваться
+ [незаметно раствориться в толпе]
    ~ Sense = Sense + 1
    Разум +1
    Не говоря ни слова, Ромео опустил голову и сделал шаг назад, позволив толпе гостей скрыть его.
    Отступая он нежно коснулся руки Джульетты, и она обернулась, продолжая смотреть на него, пока не потеряла из виду.
    Тибальт сделал несколько резких шагов вперед, но поняв, что упустил его, издал разочарованный рык и бросился вон из зала.

// Не усугублять вражду между семьями
+ [обратиться к хозяину дома с комплиментом]
    ~ Sense = Sense + 1  
    ~ Peace = Peace + 1 
    Разум +1, Мир +1
    Ромео нашел глазами главу семьи, старого Капулетти, и громко обратился прямо к нему.
    Ромео: Лорд Капулетти, я благодарю вас за гостеприимство! Ваш дом прекрасен, как и те, кто в нем живет. 
    Тибальт не ожидал этого хода и замер, не смея напасть на гостя.
    Лорд Капулетти: ... #Р
    Старый Капулетти с гордостью кивнул, бросив на племянника недовольный взгляд.
    Гости зашептались, а Ромео воспользовался замешательством, чтобы проститься с Джульеттой.
    Ромео: Я должен идти, чтобы не злить более вашего брата. Но знайте, мой ангел, вы всего дороже для меня!
    
- Когда Ромео скрылся, Джульетта подбежала к кормилице.
Джульетта: Кто этот юноша?
Кормилица: Дитя мое, его зовут Ромео, сын Монтекки.
Джульетта: Неужели правда! #У
Джульетта: Сколь мудрее было бы никогда его не встречать и не влюбляться. #П
Джульетта: Но разве я могу приказывать своему сердцу, когда оно так волнуется при мысли о нем?
Кормилица: Вам не стоит видеться. Если он будет разумен, то не будет нарушать твой покой. А скоро родители выберут тебе достойного мужа.
Кормилица: Идем, Джульетта!
Позволяя кормилице увести себя, Джульетта продолжала думать о неожиданной встрече.
-> 2_Balcony

=== 2_Balcony ===

// Встреча Ромео и Джульетты под ее балконом. Признание в любви. Договоренность о свадьбе.
// Звезды на фоне

#Location: Balcony
Сцена 2. Признание
Покинув бал, Ромео шел сквозь ночную прохладу и не мог выкинуть из головы образ Джульетты. Он не хотел допускать мысль, что покинет ее, едва повстречав, и потому он шел в сад Капулетти, надеясь продлить встречу.
Джульетта стояла на балконе, погруженная в свои мысли. Она не знала, что Ромео был в саду и слышал каждое ее слово.

Джульетта: Ромео, ах, зачем же ты Ромео?
Джульетта: Ведь имя — это просто звук, пустая оболочка, а ты — это ты. Добрый, смелый, прекрасный... И все же это имя — стена между нами. #П
Джульетта: Отрекись от своего имени! А если нет, то поклянись мне в любви, и я перестану быть Капулетти. #Р
Ромео: Ловлю тебя на слове! Назови меня своей любовью, и с этого мига я больше не Ромео. #Р
Джульетта: Ах! Как ты сюда пробрался? Стены сада высоки и неприступны. Если мои родные найдут тебя здесь — тебе верная смерть! #У
Ромео: Любовь принесла меня на своих крыльях, и каменные стены ей не преграда. #Р
Джульетта: Но их ненависть найдет тебя... #П
Ромео: Тьма ночи скроет меня от их глаз. Но даже если так, один твой нежный взгляд дал бы мне броню против сотен их клинков. #Р

Ромео подошел ближе к балкону, и Джульетта задумалась, 
Союз с Ромео был опасен, но она уже не хотела представлять свою жизнь без него.
 
Джульетта: Я хочу...
// Критический выбор
+ [довериться сердцу и признаться в любви]
    ~ Passion = Passion + 1
    Страсть +1
    #Location: Balcony_kiss
    Джульетта: К чему сомнения, если я уже знаю ответ в своем сердце? #Р
    Джульетта: Любовь к тебе как вспышка молнии, которая озарила мою жизнь. Ромео, любишь ли ты меня? #Р
    Ромео: Душа моя, моя любовь к тебе - единственный свет в моей жизни. До встречи с тобой я блуждал во тьме. #Р
    Ромео: Ты — само Солнце, вокруг которого теперь вращается моя вселенная, и без тепла которого я погибну. #Р
    Ромео взобрался на балкон и поцеловал Джульетту.
    Джульетта: Возьми мою любовь в обмен на твою. Я отдаю тебе свое сердце, свою судьбу, все, что у меня есть! #Р
    // Картинка, как Ромео целует руку Джульетты

+ [проверить серьезность намерений Ромео]
    ~ Sense = Sense + 1
    Разум +1
    Джульетта: Это чувство так внезапно, так похоже на вспышку молнии, которая гаснет, не успев осветить тьму.
    Джульетта: Я боюсь, что наша радость так же быстро исчезнет.
    Ромео: Клянусь священным ликом луны, что серебрит верхушки этих деревьев...
    Джульетта: О, не клянись луной, изменчивой луной! Твоя любовь может оказаться такой же непостоянной.
    Джульетта: Ромео, если твои намерения честны, а цель твоя — брак, то завтра я пришлю к тебе своего человека.
    Джульетта: Передай с ним, где и когда ты хочешь совершить обряд.
    // Картинка Ромео под балконом
    
+ [поделиться с Ромео переживаниями об их семьях]
    ~ Sense = Sense + 1  
    ~ Peace = Peace + 1 
    Разум +1, Мир +1
    Джульетта: Ромео, наша любовь — хрупкий цветок, выросший на каменистой почве ненависти. Мы не сможем быть счастливы, пока наши семьи ненавидят друг друга.
    Ромео: Ты права, мой ангел! От этого она еще более драгоценна. Мы обязательно найдем способ остановить их вражду!
    Джульетта: Боюсь, нам не справиться вдвоем. Нам нужен мудрый советник, который проведет и укажет дорогу.
    Ромео: Отец Лоренцо! Он свят и видит дальше, чем мы. Встретимся у него завтра!
    Джульетта: Да, Ромео! Иди к нему на рассвете. А я, как только будет возможно, пришлю к тебе верного человека.
    // Картинка Ромео под балконом
    
- Ромео: Завтра же я договорюсь об обряде венчания! Я не хочу более и дня жить без тебя! #Р
Джульетта: Тогда лети! Скорее бы прошла ночь, приближая этот сладостный день! #Р
    
-> 3_Wedding

=== 3_Wedding ===
// Влюбленные убеждают отца Лоренцо тайно поженить их, а он проводит им проверку.

#Location: Lorenco
Сцена 3. Испытание отца Лоренцо
Монах стоял у икон и молился. Свет Луны пробивался через узкое окно кельи, освещая пучки сушеных трав и старинные книги. 
Ромео: Джульетта! #Р
Джульетта: Ромео! #Р

Отец Лоренцо: Дети мои. Вы пришли ко мне, ища таинства, которое свяжет вас навек. 
Отец Лоренцо: Но любовь, рожденная в спешке, часто приводит к горькому финалу. 
Отец Лоренцо: Я служу Господу, а не слепой страсти. Прежде чем я соглашусь стать соучастником этого рискованного союза, вы должны ответить на мои вопросы.
Отец Лоренцо: Отвечайте честно, ибо вы отвечате не только мне, но и перед Богом.
-> passion_test
== passion_test ==
Отец Лоренцо: Вопрос первый тебе, Джульетта. О вашей любви.
Отец Лоренцо: Любовь, рожденная во взгляде, хрупка. Что доказывает, что ваша связь — не просто мимолетный каприз, а сила, способная сокрушить стены, что воздвигли ваши семьи?
Джульетта: ...
+ [Наша любовь дает нам силы действовать!]
    ~ Passion = Passion + 1
    ~ Sense = Sense + 1
    ~ Lorenco = Lorenco + 1
    Страсть +1, Разум +1
    Джульетта: Любовь уже заставила его рискнуть жизнью, а меня — пойти против воли родителей.
    Джульетта: Наши чувства сильны, но это и наш осознанный выбор. Мы выбираем друг друга вопреки всему. Мы понимаем риск, но готовы многим пожертвовать ради того, чтобы быть вместе.
    
+ [Он занимает все мои мысли!]
    Джульетта: Я чувствую любовь всем сердцем! Когда я с ним, ничего другого не существует. Наша любовь сожжет любые преграды!
    
+ [Мы просто хотим быть вместе!]
    Джульетта: Разве этого не может быть достаточно? Мы никому не желаем зла.
 
- Лоренцо выслушал Джульетту, не изменившись в лице, и задал следующий вопрос Ромео.
   -> sense_test
== sense_test ==
Отец Лоренцо: Второй вопрос о вашем будущем. Пламя влюбленности согревает, но со временем оно утихает, оставляя угли. 
Отец Лоренцо: Жизнь — это не только прогулки под луной, но и совместные будни, полные забот. Что вы будете делать, когда восторг пройдет и останется лишь привычка?
Ромео: ...
+ [Мы будем чтить свой долг.]
    Ромео: Чувства приходят и уходят, святой отец. Но долг, скрепленный браком, остается.
    Ромео: Мы будем партнерами. Мы будем вести общее хозяйство и уважать друг друга, как того требует священный союз. Наша связь будет прочной, потому что она будет основана на ответственности.
    
+ [Наша любовь изменится, но не угаснет.]
    ~ Sense = Sense + 1
    ~ Passion = Passion + 1
    ~ Lorenco = Lorenco + 1
    Разум +1, Страсть +1
    Ромео: Вы правы, отец, пламя не может гореть вечно. Но когда оно утихнет, останется не пепел, а глубокое, ровное тепло.
    Ромео: Восторг сменится доверием, страсть — нежной дружбой, а прогулки под луной — уверенностью, что рядом есть рука, на которую можно опереться в любой буре.
    Ромео: Мы будем строить не костер, который гаснет, а очаг, который согреет нас до самой старости.
    
+ [Наша любовь не может угаснуть, святой отец!]
    Ромео: Это пламя, что касается небес! Привычка — это удел других, а не нас. Мы будем смотреть друг на друга с тем же восторгом до последнего вздоха!
    
- Лоренцо выслушал Ромео, по-прежнему не меняя выражения лица.
-> peace_test
== peace_test ==
Отец Лоренцо: Вопрос третий. О вашей цели. Этот брак в глазах Господа может стать лекарством для ран Вероны. 
Отец Лоренцо: Но в глазах людей он — искра в пороховой бочке. Как вы объявите семьям о вашем браке?

+ [Нам нет дела до их войны.]
    Ромео: Мой дом теперь - это Джульетта. Мы будем вместе, куда бы судьба нас ни завела.
    Джульетта: Даже если нам придется покинуть наши семьи, я готова прожить свою жизнь с Ромео.
+ [Как только они увидят нашу любовь, они помирятся!]
    Джульетта: Их сердца растают, и они не смогут больше враждовать!
    Ромое: Я не подниму меча против семьи Джульетты и, уверен, они поступят соответственно.
+ [Мы готовы стать первой нитью, сшивающей рану между нашими домами.]
    ~ Peace = Peace + 1 
    ~ Lorenco = Lorenco + 1 
    Мир +1    
    Ромео: Вражда длится так давно, что никто уже и не помнит, с чего она началась. Настало время ее остановить.
    Джульетта: Нам придется проявить терпение и смелость. Но мы готовы попытаться, ведь мы являемся продолжением наших семей.
- Лоренцо выслушал их и приготовился дать ответ.
-> final_test
== final_test ==
// Поженятся ли герои
{Lorenco == 0:
    // Лоренцо не убежден и отказывается заключать брак.
    Отец Лоренцо: Простите, дети мои, но я не стану орудием вашей погибели. 
    Отец Лоренцо:  Ваши ответы показали мне лишь слепую страсть и детские мечты. Ваш союз не принесет мира, он разожжет еще больший пожар. 
    Отец Лоренцо: Вы не готовы. Возвращайтесь в свои дома и молитесь о разуме, а не о любви. В браке вам отказано.
    Джульетта: Я боялась, что вы скажете именно так, святой отец... #П
    Ромео: Не бойся! Я по-прежнему верен своему слову!
    Ромео: Наша любовь найдет способ прорасти, и мы соединим себя узами брака, когда будем готовы. #Р
    Ромео: Мы должен идти, но время разлуки я буду мерить не ходом Солнца, а мыслями о тебе. И знай, что этих мыслей будет так много, что они приблизят нашу следующую встречу.
    -> final_test_end
}
{ Lorenco == 3:
    // Лоренцо убежден и заключает брак.
    Отец Лоренцо: Сам Господь говорит вашими устами. В ваших ответах я вижу не только любовь, но и мудрость, и смирение.
    Отец Лоренцо: Этот союз благословлен. Пойдемте, дети мои. Я обвенчаю вас немедленно.
    ~ Juliette = "Romeo"
    ~ Sense = Sense + 1
    ~ Passion = Passion + 1
    ~ Peace = Peace + 1
    Страсть +1, Разум +1, Мир +1
    -> wedding
}
{ Lorenco > 0:
    // Лоренцо сомневается и уточняет, намерены ли герои заключить брак.
    Отец Лоренцо: В ваших словах есть и правда, и опасная наивность. Ваше желание искренне, но фундамент вашего будущего шаток. 
    Отец Лоренцо: Я вижу любовь, но не вижу полной готовности к последствиям.
    Отец Лоренцо: Скажите мне честно, осознавая все риски, вы по-прежнему просите меня обвенчать вас?
    
    + [Заключить брак]
        ~ Juliette = "Romeo"
        ~ Sense = Sense + 1
        ~ Passion = Passion + 1
        ~ Peace = Peace + 1
        Разум +1, Страсть +1, Мир +1
        Ромео: Да, мы готовы ко всему!
        Джульетта: Лишь ему я готова вручить свою судьбу!
        Отец Лоренцо: Да будет так. И да поможет нам всем Господь.
        -> wedding
    + [Отложить брак]
        Джульетта: Вы правы, отец Лоренцо, нам нужно еще подумать.
        Ромео: Наша любовь найдет способ прорасти, и мы соединим себя узами брака, когда будем готовы.
        Ромео: Мы должны идти, но время разлуки я буду мерить не ходом Солнца, а мыслями о тебе. И знай, что этих мыслей будет так много, что они приблизят нашу следующую встречу.
        -> final_test_end
}
== wedding ==
// Картинка свадьбы
#Location: Wedding
Клятвы Ромео и Джульетты были не громкими речами для гостей, а тихим шепотом, предназначенным только друг для друга и для Бога. 
Он взял ее руку, холодную от волнения, и почувствовал, как ее пальцы крепко сжали его в ответ. 
Отец Лоренцо произнес слова, которые превратили двух врагов по рождению в единое целое по воле небес. 
-> final_test_end
== final_test_end ==
// В итоге влюбленные расходятся по домам.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
-> 4_Mercucio_death

=== 4_Mercucio_death ===
// Ромео возвращается от отца Лоренцо и видит своих друзей Меркуцио и Бенволио, которые встретилсь с Тибальтом. Конфликт переходит в кровавую фазу.

#Location: Verona
Сцена 4. Роковая встреча

Ромео шел по улицам Вероны, и его сердце наполняла тайная радость, которая делала весь мир новым и ярким.
На центральной площади он увидел своих друзей, Меркуцио и Бенволио, которые столкнулись с Тибальтом.

Тибальт: Где твой хозяин, Монтекки?  #З
Меркуцио: Ромео? Вы ищете синьора Ромео? #У
Меркуцио: Боюсь, его расписание на сегодня уже заполнено. Но я могу записать вас в лист ожидания. #Р
Бенволио: Меркуцио, жара сводит людей с ума, а Капулетти ищут ссоры. Будем мудрее, уйдем.
Меркуцио: Ты называешь мудростью страх, одетый в красивые слова. Но честь — это огонь в крови, который требует ответа на оскорбление. Молчать — значит позволить этому огню погаснуть и умереть внутри. #З
Бенволио: Ты путаешь честь с гордыней, мой друг. Любой пес может ответить на рык рыком — это инстинкт, а не доблесть. Настоящая честь — это быть хозяином своей страсти, а не ее рабом.
Меркуцио: Красиво сказано! Но пока ты будешь хозяином своей страсти, Тибальт проткнет твое сердце своей. Твоя мудрость умрет вместе с тобой. А я предпочитаю, чтобы мой клинок говорил за меня и за моих друзей, пока я еще могу его держать. 

Тибальт: А вот и ты, Ромео! Подлец, что посмел явиться в мой дом прошлой ночью. #У
Тибальт: Ты должен поплатиться за это, доставай свою шпагу. #З

Ромео: Тибальт, есть причина, по которой я должен любить тебя. Она позволяет мне простить твою ярость и  оскорбление.
Ромео: Я не подлец. А потому прощай, я вижу, ты не знаешь, кто я.

Меркуцио: Что за позорное, трусливое смирение! Слышишь, Бенволио? Голубиная нежность там, где должен быть рык льва! #У
Меркуцио: Честь требует отмщения! Но раз уж наш Ромео сегодня решил стать святым, мне придется исполнить роль грешника. Ну же, Тибальт, о Принц Кошек, потанцуй со мной!
Тибальт: Ну что ж, Меркуцио. Раз ты так рвешься в бой, я к твоим услугам.
Тибальт: Давно я не танцевал с шутом, чей язык острее шпаги.

Шпаги со звоном скрестились. 
Меркуцио, со смехом и шутливыми выкриками, легко парировал выпады, его клинок двигался с игривой грацией. 
Тибальт, с холодной ухмылкой, отвечал ему такими же отточенными, почти театральными движениями.
Ромео в ужасе смотрел, как они обмениваются уколами, которые проносились в сантиметре от груди. 
Ромео: Стойте! Именем Герцога, прекратите! #У
Ромео в ужасе бросился между фехтующими.
Пытаясь их разнять, Ромео встал так, что на мгновение закрыл Меркуцио обзор.
Этого хватило, чтобы Тибальт нанес смертельный удар под рукой Ромео.

// Слова Меркуцио перед смертью
Меркуцио упал на руки Ромео. Он прижимал руку к полученной ране, ясно осознавая ее серьезность.
Меркуцио: Я сделал это ради тебя, понимаешь? #П
Меркуцио: Я не мог просто стоять и слушать, как он оскорбляет тебя, а ты молчишь. Кто-то должен был защитить твою честь, раз уж ты сам не стал. #З
Меркуцио: Но теперь... теперь из-за этой глупой, бесконечной вражды я умираю. #П
Меркуцио: Чума... чума на оба ваших дома! #П
Меркуцио затих навсегда.

Счастье, переполнявшее Ромео всего минуту назад, сменилось ледяным горем и обжигающей яростью.
Кровь его лучшего друга взывала к отмщению. Но клятва, данная Джульетте, вызывала к миролюбию.

// Критический выбор

// Классический путь, ведущий к изгнанию.
+ [Отомстить немедленно, атаковав Тибальта] 
    ~ Tibalt = "Dead"
    ~ Romeo = "Exit"
    ~ Passion = Passion + 1 
    Страсть +1
    Ромео: Тибальт! Убийца! #З
    Тибальт: ... #У
    Ярость затмила разум Ромео. С громким криком он вытащил шпагу и нанес удар.
    Бой был коротким - движимый горем, Ромео оказался быстрее. Тибальт упал замертво к его ногам.
    Ромео замер, глядя на то, что наделал. Шпага выпала из его ослабевших пальцев. Огонь ярости, что кипел в нем, погас, оставив после себя лишь холодный ужас. 
    Не просто враг... а двоюродный брат Джульетты.
    Он отомстил за Меркуцио, но какой ценой? Одним ударом он уничтожил их будущее. 
    Как она теперь посмотрит на него? Он — убийца ее брата.
    Ромео: О, я игрушка судьбы! #П
    Бенволио: Беги, Ромео! Герцог казнит тебя! #У
    Ноги Ромео словно вросли в раскаленные камни площади. Он смотрел на два тела у своих ног — друга и врага — и не мог сдвинуться с места.
    Бенволио: Ромео, очнись! Слышишь? Городская стража уже близко! Если они найдут тебя здесь, все будет кончено! #У
    Слово "кончено" вырвало Ромео из оцепенения, и он побежал, не разбирая дороги.
    -> 5_Farewell
    
// Альтернативные пути, доступные по статам.
+ {Sense >=3} [Сдержать гнев и потребовать правосудия у Герцога]
    ~ Tibalt = "Exit"
    ~ Sense = Sense + 1 
    Разум +1
    Ромео опустил руку, не давая ярости взять верх.
    Ромео: Я не стану убийцей. Но он ответит за это!
    Когда прибыла городская стража, Ромео указал на убийцу.
    Ромео: Вот он! Тибальт из дома Капулетти убил Меркуцио, родича Герцога! Я требую правосудия!
    Стражники, увидев герб Меркуцио, без колебаний окружили Тибальта.
    Тибальт с яростью и презрением посмотрел на стражников, а затем на Ромео.
    Тибальт: Уберите от меня руки! Ты, трус, Монтекки! Прячешься за законом, вместо того чтобы драться как мужчина! Ты за это ответишь! #З
    В этот момент на площадь прибыл сам Герцог Вероны, его лицо было мрачнее тучи.
    Он увидел тело своего родственника, Меркуцио, и его голос загремел над площадью.
    Герцог: Моя собственная кровь пролита на улицах Вероны из-за вашей бессмысленной вражды! 
    Герцог: Тибальт, за убийство Меркуцио я должен был бы приговорить тебя к смерти.
    Герцог: Но поскольку Ромео, друг убитого, не стал вершить самосуд, а воззвал к закону, я тоже проявлю милосердие. 
    Герцог: Я изгоняю тебя из Вероны, Тибальт Капулетти! Если ты когда-либо будешь замечен в стенах этого города, в тот же час ты умрешь!
    Лицо Тибальта исказилось от ярости. Для такого гордого дворянина изгнание было хуже смерти. 
    Он боролся в руках стражи, его глаза горели ненавистью, устремленной на Ромео, пока стража силой уводила его с площади. 
    Ромео смотрел, как уводят Тибальта, и не чувствовал радости. 
    Да, он был жив и свободен. Меркуцио был отомщен. Но какой ценой? 
    Он только что стал причиной изгнания двоюродного брата Джульетты. Как она посмотрит на него теперь? 
    Он избежал гибели на площади, но, возможно, только что навсегда погубил их любовь.
    -> 5_2_Date
    
+ {Peace >=2} [Сдержать гнев и заклинать семьи перестать враждовать]
    ~ Parents = Parents + 1
    ~ Peace = Peace + 1 
    Мир +1
    По краю площади стали собираться зеваки. Кто с ужасом, кто с ненавистью, кто со слезами смотрели на разыгравшуюся драму.
    Ромео обратился к толпе, где стояли и Монтекки, и Капулетти.
    Ромео: Меркуцио кричал: "Чума на оба ваших дома!", и он был прав!
    Ромео: Посмотрите, что наделала наша общая ненависть! Сколько еще должно умереть за гордыню наших отцов?
    Тибальт: Это ты! Это все из-за тебя, Монтекки! #З
    Тибальт: Если бы ты ответил на мой вызов как мужчина, он был бы сейчас жив! #З 
    Тибальт: Твоя трусость, твои лживые речи о мире — вот что заставило его обнажить шпагу! #З
    Ромео: Ты ищешь виноватого, Тибальт, и я не стану тебя за это судить. 
    Ромео: Но моя честь не в том, чтобы проливать кровь, а в том, чтобы остановить ее. 
    Ромео: Не моя трусость убила его и не твой клинок. Его убила наша ненависть.
    Ромео: Мы можем и дальше бросаться обвинениями, пока не останется никого, кто мог бы их услышать. 
    Ромео: Или мы можем остановиться. Здесь. Сейчас.
    В толпе воцарилось потрясенное молчание. Семена мира были посеяны ценой жизни Меркуцио.
    Бенволио: Городская стража уже близко! #У
    Тибальт посмотрел на безжизненное тело Меркуцио, затем снова на Ромео, и в его взгляде промелькнуло отчаяние.
    Развернувшись, он скрылся в лабиринте веронских улиц.
    -> 5_2_Date

=== 5_Farewell ===
// Ромео изгнан
#Location: Balcony
Сцена 5. На закате
Закат окрасил небо Вероны в тревожные, кровавые тона. 
Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. Он взобрался наверх.

{ Juliette == "Romeo":
    Джульетта: О, Ромео! Это правда? Тибальт... #П
    Джульетта: Как мог мой дорогой муж стать убийцей моего брата? #П
    Ромео: Он убил Меркуцио, Джульетта. Моего лучшего друга. #П
    Ромео: Я не владел собой, я был ослеплен горем! Я бы отдал все, чтобы этого не случилось. #П
    Джульетта: Мое сердце разбито на две части. Одна оплакивает брата, другая — страдает за возлюбленного. #П
    Джульетта: Но ты — мой муж. И моя верность — с тобой, до самого конца.
    -> early_bird
- else:
    Джульетта: О, Ромео! Это правда? Тибальт... мой бедный брат! #П
    Джульетта: Я смотрела на тебя и видела ангела, но неужели за этим лицом скрывался тот, кто способен на убийство? #П
    Ромео: Он убил Меркуцио, Джульетта. Моего лучшего друга. #П
    Ромео: Я не владел собой, я был ослеплен горем! Я бы отдал все, чтобы этого не случилось. #П
    Джульетта: Я должна ненавидеть тебя... Весь мой род требует этого. #П
    Джульетта: Но мое глупое сердце... оно не может. Оно все еще любит тебя. #У
    -> early_bird
}
== early_bird ==
Гнев и обида утихли, оставив после себя лишь огромную, общую на двоих печаль. 
Они знали, что это их последняя встреча. На рассвете Ромео должен был навсегда покинуть Верону, спасаясь от гнева Герцога. 
Каждая секунда вместе была украденным у судьбы сокровищем, и оба понимали, что оно утекает сквозь пальцы.

// Критический выбор
Джульетта:


+ {Passion >= 4} [Надо бежать прямо сейчас!]
    ~ Exit = true
    ~ Passion = Passion + 1
    -> 6_FINAL_Fly_away

+ [Останься еще немного!]
    ~ Passion = Passion + 1
    Страсть +1
    Джульетта: Останься еще на час, на мгновение! Я не готова прощаться! #У
    Ромео: Любовь моя, каждый миг здесь приближает ко мне смерть. Но ради еще одной минуты с тобой я готов рискнуть вечностью.
    Джульетта: Нет, Ромео! Не рискуй ради меня! Моя любовь к тебе сильнее желания удержать тебя здесь. #П
+ [Ты должен скорее уходить!]
    ~ Sense = Sense + 1
    Разум +1
    Джульетта: Моя любовь к тебе сильнее желания удержать тебя здесь. Я лучше буду жить без тебя, зная, что ты в безопасности, чем потеряю тебя навсегда из-за своей слабости. #П
    
- Ромео: Прощай, любовь моя. Живи и верь в нашу скорую встречу.
Он спрыгнул с балкона и исчез в утреннем тумане, оставив Джульетту одну, с разбитым сердцем и ужасающей пустотой впереди.

Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
-> 6_Ultimatum

=== 5_2_Date ===
// Ромео НЕ изгнан. Вместо прощания — тайная встреча. 
#Location: Balcony
Сцена 5. На закате
Закат окрасил небо Вероны в тревожные, кровавые тона.
Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. Он взобрался наверх.

{ Tibalt == "Exit":
    // Тибальт изгнан Герцогом
    Джульетта: Ромео! Ты жив, ты здесь! Я так боялась... #У
    Джульетта: Но... это правда? Тибальт... мой двоюродный брат... он изгнан из-за тебя? #П
    Ромео: Я не хотел этого, любовь моя, клянусь. #П
    Ромео: Но он убил Меркуцио. Я выбрал закон, а не месть, чтобы не запятнать нашу любовь его кровью. #П
    -> we_choose
- else:
    // Тибальт ушел с площади
    Джульетта: Ромео! Они говорят, ты стоял над телом Меркуцио и говорил о мире... 
    Джульетта: Ты был так смел, так благороден! Но Тибальт... 
    Джульетта: Я видела его глаза, когда он заходил сегодня. Он этого не простит. Теперь его ненависть будет личной. #П
    Ромео: Я знаю. Я видел это в его взгляде. Поэтому я здесь.
    -> we_choose
}
== we_choose ==
Ромео: Ненависть повсюду. На улицах, в наших домах. #П
Ромео: Единственный островок мира во всей Вероне — это мы с тобой, здесь, сейчас.
Джульетта: Но как долго мы сможем скрываться? Ждать, пока Тибальт вернется, чтобы отомстить? Или пока наши отцы решат нашу судьбу за нас?
Джульетта: Я боюсь, Ромео. Боюсь за тебя. Каждый день в этом городе для тебя теперь — смертельная ловушка. #П
Ромео: Я знаю. Поэтому мы должны решить нашу судьбу.

// Критический выбор
+ {Passion >= 4} [Надо бежать прямо сейчас!]
    ~ Exit = true
    ~ Passion = Passion + 1
    -> 6_FINAL_Fly_away

+ {Juliette == "Romeo"} [Пора объявить о нашем браке открыто!]
    ~ Tell = true
    ~ Passion = Passion + 1
    ~ Sense = Sense + 1
    ~ Peace = Peace + 1
    Разум +1, Страсть +1, Мир +1
    Джульетта: Наша любовь чиста, а наш брак благословлен. Они не смогут разорвать его.
    Ромео: Завтра встретимся у Герцога. Отец Лоренцо поможет сообщить нашим родителям, а мы объявим всем.
    Ромео поцеловал руки Джульетты и спрыгнул с балкона.
    Ромео: До завтра, душа моя!
    Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
    -> 6_FINAL_Peace

+ {Juliette != "Romeo"} [Я дождусь момента, чтобы просить твоей руки]
    ~ Sense = Sense + 1
    Разум +1, 
    Ромео: Отец просил меня съездить по делам семьи. Я вернусь через пару дней - за это время все как раз уляжется. И я приду к твоему отцу.
    Джульетта: Ты прав, все произошло так быстро. Голова идет кругом. Пара дней позволят всем успокоиться, и мы сможем построить будущее с ясной головой.
    Ромео поцеловал руки Джульетты и спрыгнул с балкона.
    Ромео: До встречи, мой ангел!
    -> 6_Ultimatum

+ {Juliette == "Romeo"} [Я дождусь момента, чтобы поговорить с твоим отцом]
    ~ Sense = Sense + 1
    Разум +1, 
    Ромео: Отец просил меня съездить по делам семьи. Я вернусь через пару дней - за это время все как раз уляжется. И я приду к твоему отцу.
    Джульетта: Ты прав, все произошло так быстро. Голова идет кругом. Пара дней позволят всем успокоиться, и мы сможем построить будущее с ясной головой.
    Ромео поцеловал руки Джульетты и спрыгнул с балкона.
    Ромео: До встречи, мой ангел!
    Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
    -> 6_Ultimatum

=== 6_FINAL_Peace ===
#Location: Duke_Hall
Сцена 6. Суд Герцога
Тронный зал был полон напряженного молчания. По одну сторону стоял клан Монтекки, по другую — Капулетти. Их разделяла не только ненависть, но и свежая кровь Меркуцио.
Герцог Эскал сидел на троне, его лицо было суровым, как у судьи, готового вынести окончательный приговор.
По его знаку Ромео и Джульетта вышли в центр зала. Рядом с ними встал отец Лоренцо.
Ромео: Ваша светлость, главы благородных семей. Мы стоим перед вами как муж и жена. Мы тайно обвенчались перед лицом Господа.
В зале взорвалась буря. Крики "Позор!", "Предательство!", "Ложь!" смешались в оглушительный гул. Герцог ударил посохом, призывая к тишине.
Герцог: Молчать! #З
{Parents == 1:
    (Родители помнят речь Ромео на площади, благодаря этому родители поддерживают своих детей)
    // (Мир высокий, родители готовы к примирению)
    -> peace_end
- else:
    // (Мир низкий, родители сопротивляются)
    (Тибальт изгнан из-за Ромео, поэтому родители Джульетты не верят ему)
    -> Montecchi_end
}
== Montecchi_end ==
Лорд Капулетти: Ты опозорила меня! Перед лицом всей Вероны! Перед нашими злейшими врагами!
Лорд Капулетти: Я лелеял тебя, защищал, готовил тебе блестящее будущее! А ты?! Ты предала нашу кровь! Ты тайно отдала себя... Монтекки?! Твой брат изгнан из-за него!
Герцог: Глупец! Ты ослеп от своей ненависти!
Герцог: Этот брак законен. И он создает ситуацию, которую ваша вражда не сможет пережить. Джульетта — единственная наследница Капулетти. Их ребенок, ваш внук, станет наследником обоих домов!
Лорд Капулетти: Я ОТРЕКАЮСЬ ОТ НЕЕ!
Джульетта: ... #П
Лорд Капулетти: Пусть все слышат! С этой минуты у меня нет дочери! Дом Капулетти не имеет к тебе больше никакого отношения!
Лорд Монтекки: Капулетти, в своем гневе ты совершил роковую ошибку. Ты выбросил ключ к своему будущему. Но то, что один дом выбрасывает, другой подбирает.
Лорд Монтекки: Иди к нам, дитя. Твой муж здесь. Твой дом теперь здесь. Отныне ты - Монтекки.

#Location: The End
Ромео и Джульетта начали новую, сложную жизнь под одной крышей с Монтекки. 
Джульетта, проявив невероятную мудрость и такт, постепенно завоевала уважение родителей Ромео, став для них не врагом, а частью их семьи.
Она помогала Леди Монтекки управлять домом, а ее знание дел Капулетти дало Лорду Монтекки невиданное преимущество в их вековой борьбе.
Ромео, видя силу и ум своей жены, перестал быть пылким юношей и превратился в достойного наследника своего дома, научившись ценить стратегию выше страсти.
Их любовь, закаленная в политических интригах, стала прочным союзом, основанным на взаимном уважении.
Их первенец, названный в честь Меркуцио, стал символом будущего дома Монтекки, которому суждено было унаследовать ослабленное и лишенное прямого наследника состояние Капулетти.
{Tibalt == "Exit":
    Лишенный дома и чести, Тибальт скитался по Италии. Он отказался от мести, понимая, что проиграл не Ромео, а собственной сестре, которая оказалась умнее и дальновиднее его.
}
Горе семьи Капулетти, потерявшей не только дочь, но и всякую надежду на будущее, было безгранично. Они медленно угасали, наблюдая, как их злейшие враги, укрепившиеся благодаря их собственной дочери, становятся самой могущественной семьей в Вероне.
История Ромео и Джульетты превратилась в легенду о том, как слепая ярость одного отца привела к падению его дома и возвышению его врага, доказав, что в войне за будущее побеждает не тот, кто сильнее, а тот, кто умеет принимать.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
КОНЕЦ
-> END

    
== peace_end ==
Лорд Капулетти: Ваша светлость... возможно, это к лучшему. Наша бесконечная война уже стоила нам слишком много.
Герцог: Наконец-то! Мудрость пришла в ваши дома!
Лорд Капулетти: Ваша светлость... Мудрость здесь ни при чем - лишь горький подсчет потерь. Меркуцио погиб на нашей площади, проклиная оба наших дома... И он был прав.

Лорд Капулетти: А Ромео известен всей Вероне за свою доблесть и станет хорошей партией моей дочери. 
Лорд Монтекки: Мой сын... я всегда видел в нем лишь мечтателя. Мальчишку, ведомого ветреными чувствами и поэзией.
Лорд Монтекки: Но ради твоей дочери он пошел в логово врага безоружным. Он встал над телом своего лучшего друга и говорил не о мести, а о мире. Он проявил не страсть юноши, а доблесть мужа.
Лорд Монтекки: Я горжусь им. И я принимаю его жену, которая смогла разглядеть в моем сыне то, чего не видел даже я. Я протягиваю свою руку, Капулетти. Не как враг, но как отец, который хочет мира для своих детей.
Герцог: Я благословляю этот союз! Пусть он станет символом новой Вероны!

{Tibalt == "ok":
    -> Tibalt_pardon_end
- else:
    -> peace_end_afterword
}
== Tibalt_pardon_end ==
Ромео: Ваша светлость, если мы действительно начинаем новую эру мира...
Ромео: ...то я прошу вас не наказывать Тибальта за его гнев, который питала наша общая ненависть.
Джульетта: Проявите милосердие, ваша светлость. Не начинайте наш мир с нового изгнания.
Тибальт: ... #У
Герцог: Тибальт Капулетти, ты стоишь здесь, потому что убил моего родича, Меркуцио.
Герцог: По закону Вероны, за это полагается изгнание. Но поскольку твой новый родственник, Ромео, просит за тебя, и во имя этого хрупкого мира, я предложу тебе выбор.
Герцог: Ты можешь публично присягнуть, что складываешь оружие и принимаешь этот мир и этот брак - и остаться в Вероне. Или отправиться в изгнание. Решай.

Тибальт:
+ [Я покину Верону]
    Тибальт: Присягнуть? Жить в мире, где Монтекки стал мне братом? Называть мужем моей кузины того, чье имя я привык проклинать?
    Тибальт: Вы называете это миром. Я называю это позором.
    Тибальт: Я выбираю изгнание. Я лучше буду жить с честью среди чужих в Мантуе, чем без нее среди родных в этой вашей "новой Вероне".
+ [Я приму мир]
    Тибальт: Монтекки, я всю жизнь считал, что честь — это обнаженный клинок. А ты показал мне, что, возможно, настоящая честь — это сила, чтобы удержать его в ножнах.
    Тибальт: Я принимаю этот мир. Не потому, что я боюсь изгнания. А потому, что сегодня мой враг преподал мне урок чести, который я не могу игнорировать.
    
- Герцог: Таков твой выбор, Тибальт. 
Герцог: А теперь вы выйдете на площадь и скрепите ваш мир перед лицом всей Вероны!
-> peace_end_afterword
== peace_end_afterword ==
#Location: The End
Ромео и Джульетта стали символами новой Вероны. Их свадьба была грандиозным праздником, на котором впервые за сто лет Монтекки и Капулетти сидели за одним столом.
Всю дальнейшую жизни они были вынуждены постоянно доказывать свою преданность примирению, жертвуя личным счастьем ради общественного блага.
Ромео стал дипломатом, а Джульетта — хозяйкой салона, где бывшие враги учились говорить друг с другом. Их пылкая любовь переродилась в зрелое партнерство, полное ответственности и усталости.
Их дети росли, зная, что на их плечах лежит непростой груз — не дать старой ненависти возродиться.
Вражда в Вероне действительно закончилась. Старики медленно прощали, а молодежь росла в городе, где имена Монтекки и Капулетти означали не войну, а союз.
История Ромео и Джульетты превратилась в героическую легенду о том, как любовь двух юных сердец оказалась сильнее вековой ненависти.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
КОНЕЦ
-> END

=== 6_FINAL_Fly_away ==
Страсть +1
Ромео: Тогда давай покинем этот мир ненависти! Бежим из Вероны сегодня же ночью! 
Джульетта: Бежать... Да. Да! Какая разница, где жить, если я буду жить с тобой?
Ромео: Мы отправимся в Мантую! Там мы окажемся свободны от наших имен. Мы скроемся и начнем и новую жизнь. 
Ромео: Я договорюсь обо всем и вернусь через два часа. Не будем медлить!
Джульетта: Два часа... #У
Джульетта: Я соберу все самое ценное, что смогу унести, — это поможет нам в первое время. 
Джульетта: Прошу, будь осторожен, моя любовь. И возвращайся скорее.

Чрезе два часа в тени сада пофыркивали две оседланные лошади. 
По брошенной им веревочной лестнице Джульетта спустилась вниз, в последний раз взглянув на свой дом, который теперь стал для нее чужим.

#Location: The End
Ромео и Джульетта добрались до Мантуи и начали новую жизнь. Они представились как Джованни и Мария Росси, наследники разорившейся из-за пожара семьи из Падуи.
Драгоценностей Джульетты хватило на то, чтобы снять скромное жилье и прожить первый месяц. Не понимая цену денег они быстро потратили их и столкнулись с необходимостью зарабатывать.
Решив никогда не возвращаться в Верону, Ромео устроился учителем по фехтованию у сына богатого ремесленника. Он стал более молчаливым и более ответственным, обеспечивая пропитание своей семьи.
Домашнее хозяйства легло на плечи Джульетты, лишенной кормилицы и слуг. Она научилась готовить из ничего, торговаться на рынке, лечить детей травами. 
Иногда по ночам, глядя на спящую жену, Ромео вспоминал ту девочку на балконе, и его сердце сжималось от нежности и вины за ту жизнь, в которую он ее привел.
Своему сыну Лоренцо, единственному дожившему до взрослого возраста, они так и не рассказали о его настоящем происхождении.
{ Tibalt == "Exit":
    Лишенный дома и чести, Тибальт скитался по Италии, снедаемый лишь одной мыслью — вернуться в Верону и омыть свой позор в крови Ромео Монтекки.
}
Горе семей Монтекки и Капулетти, лишившихся своих наследников, было безгранично. Каждый из них винил другого, что еще больше ожесточило противостояние между семьями.
История Ромео и Джульетты превратилась в мрачную легенду, которую рассказывали детям как предостережение, а их имена стали символом вражды, которая так и не нашла своего завершения.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
КОНЕЦ
-> END

=== 6_Ultimatum ===
#Location: Capuletti
// На следующей день происходит разговор с родителями.
Сцена 6. Предложение
На следующий день в комнату Джульетты вошли родители.
-> Tibalt_conv
== Tibalt_conv ==
{ Tibalt == "Exit":
    // Родители сокрушаются об изгнании Тибальта.
    Лорд Капулетти: Ох, мой несчастный племянник изгнан из-за этого проклятого Монтекки. Он сам навлек на себя беду своей горячностью. Но позор лег на весь наш дом.
    Джульетта: Тибальт был мне братом, и мое сердце разрывается при мысли о его изгнании. #П
    Лорд Капулетти: Довольно слез, дитя мое! Я принес тебе лекарство от печали, чудесную новость, что высушит твои слезы!
    Джульетта: Какую новость, отец?
    Лорд Капулетти: Достойнейший жених, молодой граф Парис, родственник Герцога, просит твоей руки. Я дал свое согласие! В четверг вы обвенчаетесь!
    -> Juliette_conv
- else:
    { Tibalt == "Dead": 
        // Родители сокрушаются о смерти Тибальта.
        Лорд Капулетти: Ох, мой несчастный племянник убит на площади! Я не успокоюсь, пока убийца не заплатит своей жизнью! #З
        Джульетта: ... #П
        Джульетта: Тибальт был мне братом, и мое сердце разрывается при мысли о том, что его больше нет. #П
        Лорд Капулетти: Довольно слез, дитя мое! Месть будет позже. Но сейчас я принес тебе лекарство от печали, чудесную новость, что высушит твои слезы!
        Джульетта: Какую новость, отец?
        Лорд Капулетти: Достойнейший жених, молодой граф Парис, родственник Герцога, просит твоей руки. Я дал свое согласие! В четверг вы обвенчаетесь!
        -> Juliette_conv
    - else: 
        {Parents == 1: 
            // Родители упоминают речь Ромео на площади.
            Лорд Капулетти: Я все думаю о том, что произошло на площади. О речи этого мальчишки Монтекки.
            Лорд Капулетти: Он говорил о "чуме на оба наших дома". Он обвинил не Тибальта, а нашу "общую ненависть". И толпа... толпа слушала его.
            Джульетта: ...
            Лорд Капулетти: Я устал от этой вражды, клянусь всеми святыми. Она отнимает у нас лучших людей и не приносит ничего, кроме горя. 
            Лорд Капулетти: Но то, что сделал этот мальчишка, — это не призыв к миру. Он выставил Монтекки благородными страдальцами, а нас, Капулетти, — кровожадными убийцами в глазах всего города и самого Герцога!
            Лорд Капулетти: Они сделали свой ход. Теперь наша очередь. И наш ответ должен быть таким, чтобы укрепить наш дом раз и навсегда. Нам нужен могущественный союзник.
            Лорд Капулетти: Граф Парис, родич Герцога, просил твоей руки. Я думал дать тебе время, но теперь времени нет. 
            Лорд Капулетти: Свадьба с ним укрепит наше положение, закроет рты всем нашим врагам и покажет Монтекки, что мы сильны как никогда.
            -> Juliette_conv
        - else: 
            Лорд Капулетти: Дочь моя! Достойнейший жених, молодой граф Парис, родственник Герцога, просит твоей руки. Я дал свое согласие! В четверг вы обвенчаетесь!
            Лорд Капулетти: Эта свадьба укрепит положение рода Капулетти и вернет праздник в наш дом!
            -> Juliette_conv
        }
    }
}
        
        
== Juliette_conv ==
// Родители хотят, чтобы Джульетта вышла замуж за Париса.
Джульетта: ... #П
Джульетта: Отец, я благодарна за вашу заботу, но... я не могу. Это слишком поспешно. #П
Лорд Капулетти: Что значит "не могу"? Я устроил твое счастье! Это мое решение. #З
Джульетта смотрела на отца с ужасом. 

{ Juliette != "Romeo": 
    Джульетта: (Но как же Ромео? Его глаза, его слова, его клятва на балконе... В тот миг я была уверена, что это судьба. Что мы созданы друг для друга.)
    { Lorenco == 0: 
        Мы просили благословения у отца Лоренцо, но он отказал нам. Он сказал, что наш союз принесет лишь горе.
    - else:
        Мы должны были обвенчаться. Это должно было сделать нас единым целым, защитить нас перед Богом и людьми. Наша любовь осталась тайной, хрупким сном, который так легко разрушить.
    }
    Джульетта: (Может быть, мне действительно стоит выйти замуж за Париса?)
    Джульетта: (Он благороден, одобрен семьей. Он предлагает мне спокойную, понятную жизнь без лжи. Я стану графиней, у меня будет дом, положение, уважение. Никто не будет называть меня предательницей.)
    Джульетта: (Стоит ли бороться за любовь, которая требует столько жертв, но не дает защиты?)
}

// Критический выбор
// Джульетта встает перед выбором

+ {Juliette != "Romeo"} [Согласиться на брак с Парисом]
    ~ Sense = Sense + 1
    ~ Juliette = "Paris"
    Разум +1
    Бремя тайны и страха оказалось слишком тяжелым для Джульетты. 
    Она сделала выбор, который разобьет ей сердце, но, возможно, спасет от еще большей беды.
    Джульетта: Отец... вы правы. Я была ослеплена горем и говорила глупости. Я принимаю вашу волю. Я выйду замуж за графа Париса.
    Лорд Капулетти: Вот это моя дочь! Разумная, послушная! Ты сделала меня самым счастливым отцом в Вероне! Ты увидишь, это правильный выбор! #Р
    Лорд Капулетти: Мы устроим такой пир, какого Верона не видела! Мы покажем этим Монтекки, что значит настоящий праздник! #Р
    -> 7_FINAL_Wedding_Paris

+ [Категорически отказаться выходить за Париса]
    ~ Passion = Passion + 1
    Джульетта: (Я не могу сказать ему правду, это погубит Ромео. Но и согласиться я не могу. Я должна просто сказать «нет» и выдержать бурю.)
    Джульетта: Отец, прошу вас! Я не выйду замуж за Париса! Я скорее умру! #П
    Лорд Капулетти: Неблагодарная девчонка! Выбирай: либо ты идешь в церковь в четверг, либо я тебя больше не знаю! #З
    Джульетта: Позвольте мне хотя бы исповедоваться перед свадьбой! У отца Лоренцо! #У
    Лорд Капулетти: Хорошо. Иди. Может, святой отец вложит в твою голову хоть каплю разума и благодарности.
    Лорд Капулетти: Но запомни: это ничего не меняет. В четверг — свадьба.
    -> 7_Lorenco_help

+ [Тянуть время]
    ~ Sense = Sense + 1
    Джульетта: (Я не хочу вызывать гнев своего отца, но я не могу согласиться. Может быть он согласится дать отсрочку?
    Джульетта: Отец, молю вас, на коленях! Не спешите со свадьбой! Ведь мы совсем не знаем друг друга. #П
    Лорд Капулетти: Никаких отсрочек! У вас будет вся жизнь, чтобы узнать друг друга. В четверг ты будешь у алтаря, или ты больше не переступишь порог этого дома.
    Джульетта: Позвольте мне хотя бы исповедоваться перед свадьбой! У отца Лоренцо! #У
    Лорд Капулетти: Хорошо. Иди. Может, святой отец вложит в твою голову хоть каплю разума и благодарности.
    Лорд Капулетти: Но запомни: это ничего не меняет. В четверг — свадьба.
    -> 7_Lorenco_help
    
+ {Juliette == "Romeo"} [Признаться, что уже замужем]
~ Passion = Passion + 1
~ Sense = Sense + 1
~ Peace = Peace + 1
Страсть +1, Разум +1, Мир +1
Джульетта: (Правда — мой единственный щит. Он разгневается, но он не сможет заставить меня нарушить клятву, данную перед Богом.)
Джульетта: Отец, я не могу выйти за Париса... потому что я уже замужем. Мой муж — Ромео Монтекки.
-> 7_Final_told_about_Romeo
    
    
=== 7_Final_told_about_Romeo ===
Лорд Капулетти: Что... ты... сказала? #У
{ Peace >= 3: 
    (У вас высокий показатель "Мир", благодаря этому у Джульетты получится убедить Лорда Капулетти.)
    Джульетта: Вы говорили о будущем нашего рода, отец. О том, как укрепить наш дом. Мой брак с Ромео — это и есть наше будущее.
    Лорд Капулетти: Будущее?! С сыном нашего врага?! Это позор! #З
    Джульетта: Нет! Это конец вражды! Подумайте! Я — ваша единственная наследница. Он — наследник Монтекки. 
    Джульетта: Наш ребенок... ваш внук... он объединит оба дома! Он унаследует все! 
    Джульетта: Не убивайте ваше собственное наследие из-за вражды, которая вас самого тяготит!
    Ее слова заставили Лорда Капулетти замереть. Он посмотрел на жену, ища поддержки в своей ярости, но увидел в ее глазах не гнев, а страх.
    Лорд Капулетти долго молчал, его лицо было полем битвы. Наконец, он с шумом выдохнул, и его плечи опустились. Он выглядел постаревшим на десять лет.
    Лорд Капулетти: Ты разорвала наш союз с графом Парисом. Ты опозорила меня перед Герцогом. Ты отдала наследницу дома Капулетти нашему злейшему врагу.
    Лорд Капулетти: Я не могу простить тебе, что ты сделала это без моего ведома. Но то, что сделано, сделано. И раз уж ты начала эту игру, мы доведем ее до конца. По-нашему.
    Лорд Капулетти: Жена, позови отца Лоренцо. Немедленно. Он должен будет подтвердить каждое слово перед Герцогом. И пошли весточку старому Монтекки. Передай, что Лорд Капулетти будет говорить с ним.
    
    Сцена 7. Суд Герцога
    Тронный зал был полон напряженного молчания. По одну сторону стоял клан Монтекки, по другую — Капулетти. Их разделяла не только ненависть, но и свежая кровь Меркуцио.
    Герцог Эскал сидел на троне, его лицо было суровым, как у судьи, готового вынести окончательный приговор.
    По его знаку Ромео и Джульетта вышли в центр зала. Рядом с ними встал отец Лоренцо.
    Ромео: Ваша светлость, главы благородных семей. Мы стоим перед вами как муж и жена. Мы тайно обвенчались перед лицом Господа.
    В зале взорвалась буря. Крики "Позор!", "Предательство!", "Ложь!" смешались в оглушительный гул. Герцог ударил посохом, призывая к тишине.
    Герцог: Молчать!
     -> peace_end
- else:
    (У вас низкий показатель "Мир", Лорд Капулетти не воспримет доводы Джульетты.)
    Джульетта: Отец, я сделала это ради любви! Я люблю его!
    Лорд Капулетти: Любовь?! Ты опозорила меня! Опозорила имя Капулетти!
    Лорд Капулетти: Я тебе больше не отец. У тебя нет ни имени, ни дома. Ты выбрала своего Монтекки — так иди к нему! Я лишаю тебя всего — наследства, титула, имени! 
    Джульетта: Отец, нет! #П
    Лорд Капулетти: Вон из моего дома! И чтобы я тебя больше никогда не видел! Ты для меня умерла! #З
    Он развернулся и вышел, не оглядываясь, его шаги гулко отдавались в тишине. 
    Родители отреклись от нее.Теперь у нее не было никого, кроме надежды на мужа и помощь отца Лоренцо.
    
    Сцена 7. Суд Герцога
    Тронный зал был полон напряженного молчания. По одну сторону стоял клан Монтекки, по другую — Капулетти. Их разделяла не только ненависть, но и свежая кровь Меркуцио.
    Герцог Эскал сидел на троне, его лицо было суровым, как у судьи, готового вынести окончательный приговор.
    По его знаку Ромео и Джульетта вышли в центр зала. Рядом с ними встал отец Лоренцо.
    Ромео: Ваша светлость, главы благородных семей. Мы стоим перед вами как муж и жена. Мы тайно обвенчались перед лицом Господа.
    В зале взорвалась буря. Крики "Позор!", "Предательство!", "Ложь!" смешались в оглушительный гул. Герцог ударил посохом, призывая к тишине.
    Герцог: Молчать!
    -> Montecchi_end
}

== 7_FINAL_Wedding_Paris == 
#Location: Wedding_Paris
Сцена 7. Свадьба
Церковь была залита светом и полна гостей. Джульетта, одетая в роскошное свадебное платье, похожее на драгоценный саван, шла к алтарю под руку с отцом.
Ее лицо было прекрасно, но неподвижно, как у мраморной статуи. Она делала то, что должна была. Она выбрала долг, безопасность и волю своей семьи.
Граф Парис ждал ее у алтаря, его лицо сияло от гордости и счастья. Он видел перед собой прекрасную невесту и блестящее будущее.
Парис: ...
Отец Лоренцо: Если кто-то знает причину, по которой этот брак не может быть заключен, пусть говорит сейчас или молчит вечно.

{Passion >= 4 or Juliette == "Romeo":
    (Вы идете по пути страсти, Ромео не может допустить этой свадьбы)
    Двери церкви с грохотом распахнулись. На пороге стоял Ромео, его глаза горели отчаянием.
    Ромео: Я возражаю! #З
    Парис: Монтекки! Как ты смеешь?! Стража! #З
    {Juliette == "Romeo":
        Ромео: Эта свадьба обман! Она моя жена!
    - else:
        Ромео: Эта свадьба обман! Ее сердце принадлежит мне! #З
    }
    Парис: Ты лжец! опозорил мою невесту и мой дом! За это ты умрешь!
    Звон стали эхом разнесся под сводами церкви. Бой был коротким и яростным. Ромео был пойман на ответном выпаде Париса и упал на каменные плиты у самых ступеней алтаря. Джульетта закричала и бросилась к телу Ромео.
    {Juliette == "Romeo":
        Джульетта: Ромео, муж мой! Не делай меня вдовой! #П
    - else:
        Джульетта: Ромео, возлюбленный мой! Не умирай! #П
    }
    Ромео: Джульетта... прости... #П
    Ромео: Меркуцио... был прав... Чума на оба дома... #П
    Тело Ромео обмякло на коленях Джульетты. Она обняла его, и ее свадебное платье окрасилось в багровый цвет.
    -> final_wedding_tragedy_afterword
- else:
    (Вы идете по пути разума, Ромео принимает неизбежность свадьбы Джульетты и Париса)
    В церкви повисла тяжелая, звенящая тишина.
    Какая-то часть Джульетты надеялась, что двери распахнутся. Но они остались закрыты. Он прочитал ее записку и не пришел.
    Отец Лоренцо: Согласна ли ты, Джульетта, взять в мужья графа Париса?
    Джульетта сделала глубокий вдох, прощаясь с той девочкой, которой была на балконе.
    Джульетта: Да. Согласна. #З
    Кольцо холодом легло ей на палец. Она стала графиней Парис.
    -> final_bitter_peace_afterword
}
== final_wedding_tragedy_afterword ==
#Location: The End
Ромео и Джульетта так и не начали свою новую жизнь. Его любовь, не нашедшая покоя, привела его на ступени алтаря, где он и погиб от руки жениха своей возлюбленной.
Джульетта стала женой убийцы своей единственной любви. Она до конца своих дней жила в роскоши и почете, но ее сердце осталось мертвым в тот день у алтаря. Каждый взгляд на мужа был для нее молчаливым напоминанием о том, кого она потеряла.
Парис был оправдан Герцогом. Он защищал свою честь и свою невесту. Он получил все, чего хотел, но так и не смог понять, почему его прекрасная жена смотрит на него такими пустыми глазами.
Горе семьи Монтекки было безграничным. Их единственного сына убили на глазах у всего города, и они поклялись отомстить, когда восстановят силы.
Семья Капулетти отпраздновала свою победу. Они заключили выгодный союз с родственником Герцога и укрепили свое положение. 
История Ромео и Джульетты превратилась в жестокое предостережение о том, как страсть, не знающая границ, превращает любовь не в спасение, а в оружие самоуничтожения.
-> END
== final_bitter_peace_afterword ==
#Location: The End
Ромео и Джульетта прожили каждый свою жизнь.
Джульетта стала графиней, одной из самых уважаемых и влиятельных дам Вероны. Она была верной женой и любящей матерью. Ее жизнь была спокойной, богатой и абсолютно безопасной.
{Romeo == "Exit":
    Ромео остался в Мантуе. Он женился на благородной девице, стал главой своего рода, мудрым и уважаемым, но его жена чувствовала, что какая-то его часть осталась в другом городе давным-давно.
- else:
Ромео остался в Вероне. Он женился на благородной девице, стал главой своего рода, мудрым и уважаемым, но его жена чувствовала, что какая-то его часть осталась далеко от нее.
}
Иногда, в тихие ночи, Ромео и Джульетта оба смотрели на одну и ту же луну и вспоминали о давнем вечере на балконе, о тайных клятвах и о той жизни, которая могла бы быть.
Вражда между Монтекки и Капулетти постепенно утихла, превратившись в холодное, презрительное соседство.
История Ромео и Джульетты не стала легендой. Она просто не случилась, оставшись лишь тайной двух сердец, выбравших долг вместо страсти.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
КОНЕЦ
-> END

=== 7_Lorenco_help === 
#Location: Lorenco
Когда Джульетта прибежала в келью брата Лоренцо, ее лицо было белее мела. Она бросилась к ногам священника.

{ Juliette == "Romeo":
// Она замужем за Ромео
    Джульетта: Отец Лоренцо, помогите! Родители заставляют меня выйти за Париса в четверг! #П
    Джульетта: Я не могу нарушить клятву, данную перед Богом! Я не могу стать женой двоих, это смертный грех! #П
- else:
// Она не замужем за Ромео
    Джульетта: Отец Лоренцо, помогите! Родители заставляют меня выйти за Париса в четверг! #П
    Джульетта: Я не люблю его! Мое сердце принадлежит Ромео, я дала ему слово! Я не могу предать его и себя! #П
}

- Джульетта: Я не знаю, что делать! Мои родители не слушают меня, а времени почти не осталось! #П

Отец Лоренцо: Успокойся, дитя! Пока есть жизнь, есть и надежда. 
Отец Лоренцо: Но путь, что я предложу, потребует от тебя всей твоей смелости.
Он достал из деревянного ларца маленький темный флакон.
Отец Лоренцо: Выпей это зелье сегодня ночью одна. Оно погрузит тебя в сон, так похожий на смерть, что даже лекарь не отличит.
Отец Лоренцо: Твои родные отнесут тебя в фамильный склеп. А я тем временем пошлю гонца к Ромео с письмом, где все объясню.
Отец Лоренцо: Он вернется, и вы вместе скроетесь в Мантуе навсегда.

Джульетта смотрела на темный флакон.

Джульетта: Я согласна...
+ {Passion >= 4} [выпить снадобье]
// ПРОВЕРИТЬ СТРАСТЬ
    ~ Passion = Passion + 1
    Джульетта: Дайте его мне! Если это поможет мне снова быть с Ромео, я согласна!
    Отец Лоренцо проводил ее до дверей кельи, молясь, чтобы план удался.
    -> 8_FINAL_CLASSIC

+ [только отправить гонца к Ромео, не пить снадобье]
    ~ Sense = Sense + 1
    Джульетта: Святой отец, я не могу. Это безумие! Молю вас, просто пошлите гонца к Ромео! #П
    Джульетта: Скажите ему, что меня выдают замуж! Он примчится, я знаю! Он спасет меня! #У
    Отец Лоренцо: Дитя, это слишком рискованно. Гонец может не успеть...
    Джульетта: Я буду надеяться. Это все, что у меня осталось.
    Отец Лоренцо: Пусть будет так.
    -> 7_FINAL_Wedding_Paris

=== 8_FINAL_CLASSIC ===
#Location: Cripta
В тишине своей комнаты, освещенной лишь дрожащей свечой, Джульетта осушила флакон одним глотком, и холодный сон, похожий на смерть, мгновенно окутал ее, остановив ее сердце и дыхание.
Верона, что готовилась к свадьбе, проснулась под скорбный и медленный звон церковного колокола. Свадебные гирлянды, которыми начали украшать улицы, сменились черными траурными лентами.
А в Мантуе Ромео ждал вестей. Но судьба оказалась жестока: верный слуга с ложной вестью о смерти Джульетты нашел его прежде, чем в Мантую добрался гонец отца Лоренцо с письмом, где была вся правда. 
Ослепленный горем, Ромео не стал ждать подтверждения. Он нашел аптекаря, купил у него самый быстрый яд и помчался в Верону, чтобы исполнить свою последнюю клятву — соединиться с любимой в смерти.
Утром в фамильном склепе Капулетти нашли двух юных возлюбленных. У каменного ложа Джульетты, лежал Ромео, а рядом с ним — пустой флакончик из-под яда. На его груди, обнимая его даже в смерти, лежала Джульетта, с его кинжалом в сердце.
Когда главы обоих враждующих домов вошли в склеп, перед лицом безмолвных тел своих детей, их вековая ненависть наконец обратилась в прах. Они проиграли.

#Location: The End
Ромео и Джульетта так и не начали свою новую жизнь. 
Их любовь, вспыхнувшая как звезда, погасла в фамильном склепе Капулетти, став последней и самой страшной жертвой вековой вражды.
Ромео, поверив в смерть своей возлюбленной, выпил яд, чтобы соединиться с ней в вечности. Джульетта, проснувшись и увидев его бездыханное тело, заколола себя его кинжалом, отказавшись жить в мире, где нет его любви.
Их смерть не была напрасной. Увидев своих детей, лежащих бок о бок, старые Монтекки и Капулетти наконец прозрели.
Горе двух семей, лишившихся своих наследников, было безгранично. Но оно было общим. Перед лицом этой невосполнимой утраты они пожали друг другу руки над телами своих детей.
Вражда, которую не могли остановить ни указы Герцога, ни пролитая кровь, была окончена ценой жизни двух влюбленных.
История Ромео и Джульетты превратилась в самую печальную повесть на свете, которую рассказывали как вечное напоминание о том, какую страшную цену порой приходится платить за мир.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}
КОНЕЦ
-> END
