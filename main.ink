-> Start

// Переменные
VAR Passion = 0 // Один из ключевых параметров пары - страсть. Путь страсти ведет к классическому трагичному финалу.
VAR Sense = 0 // Один из ключевых параметров пары - разум. Путь разума ведет к альтернативному финалу. В зависимости от очков страсти по пути разума пара может сбежать и остаться вместе (чтобы не впасть в отчаяние и организовать побег, нужно еще немного разума) или расстаться.
VAR Peace = 0  // Один из ключевых параметров пары - стремление к миру между семьями. Путь мира может привести к тому, что влюбленные останутся живы и помирят свои семьи.
VAR Lorenco = 0 // Показатель убежденности отца Лоренцо в готовность пары к браку.
VAR Tibalt = "ok" // Тибальт может умереть или отправиться в изгнание.
VAR Romeo = "ok" // Ромео может умереть или отправиться в изгнание, а также сбежать.
VAR Juliette = "ok" // Джульетта может умереть, а также сбежать или выйти замуж (обозначается именем мужа, например "Romeo" или "Paris").
VAR Exit = false // На старте игры Джульетта не согласилась на побег, но мы запомним, если она это сделает.
VAR Tell = false // На старте игры Джульетта не согласилась рассказать всем о тайном браке с Ромео, но мы запомним, если она это сделает.
VAR Parents = 0 // Показатель убежденности родителей, что нужно мириться.

=== Start ===
#Location: Start
Всем нам знакома история Вероны — города, где давняя вражда двух равноуважаемых семей, Монтекки и Капулетти, породила самую печальную историю на свете.
Но сегодня судьба Ромео и Джульетты в ваших руках. Вам решать, какие слова будут сказаны, какие поступки совершены и к каким последствиям это приведет.
Какой финал ждет героев вашей истории?
-> 1_Ball

=== 1_Ball ===
// Первая встреча Ромео и Джульетты на балу. Первое столкновение Ромео с Тибальтом.
#Location: Verona
Сцена 1. Бал-маскарад
Воздух ночной Вероны гудел от музыки - древнейший род Капулетти давал грандиозный бал-маскарад.
Ромео Монтекки знал, что ему не стоило приближаться к этому дому.
Но надежда увидеть среди гостей свою возлюбленную Розалину влекла его как магнит.

Ромео: Какая удача, что на бал-маскарад можно надеть маску и затеряться среди танцующих.
Ромео: Я проскользну в их дом незамеченным, найду ту, что пленила мое сердце, и исчезну до того, как кто-либо заподозрит, что в их доме побывал враг.

Меркуцио: Ну же, Ромео! Хватит вздыхать о той, что не смотрит на тебя!
Меркуцио: Первые красавицы Вероны соберутся на балу - открой глава и взгляни на них!
Меркуцио: Покажи всем, что Монтекки не боится ни врагов, ни любви!

Бенволио: Меркуцио, твои речи горячи, но безрассудны.
Бенволио: Мы идем в логово врага и должны сохранять холодную голову.

Ромео: Ах, милые друзья! У меня такое чувство, что беда уже на пороге, и ее не миновать. #П
Ромео: Я последую твоему совету...  
+ [Меркуцио. Я найду свою любовь во что бы то ни стало!]
    ~ Passion = Passion + 1
     Страсть +1
+ [Бенволио. Я буду осмотрителен и буду держать себя в руках.]
    ~ Sense = Sense + 1  
     Разум +1
- Ромео: Нам нужно спешить, если хотим попасть на бал.

#Location: Ball
Как только друзья вошли в зал, их закружила карусель пляшущих и смеющихся гостей.
Взгляд Ромео сосредоточенно скользил по танцующим парам. 
И вдруг, сквозь всю эту пеструю суету, он увидел ЕЕ.

Джульетта: ...

Ромео: Как она сияет! Она затмила даже свет факелов! #Р
Ромео: Неужто я любил? До этой минуты я и не знал, что такое любовь! #У
Ромео: Кто она? Как ее зовут? Я должен подойти к ней!

Джульетта: ... #Р
    // Заметила Ромео

#Location: Romeo_and_Juliette_Ball
Ромео: Простите мою дерзость, мне показалось, что весь этот зал на мгновение замер. #Р
Джульетта: Я бы сказала, не замер, а просто… исчез. Остался лишь гул, как от далекого моря. #Р
Ромео: Для меня он превратился в неразборчивый шум, в котором лишь одна ясная нота — Ваша улыбка. #Р
Джульетта: А я чувствовала себя потерянной в этой череде лиц, пока Ваш взгляд не стал для меня маяком. #Р
Ромео: Значит, это мгновение мы украли у мира вместе. #Р
  // Картинка Ромео и Джульетты держатся за руки

#Location: Ball
Тибальт: Этот голос… Клянусь честью, это Ромео Монтекки! #У
Ромео: ...
Тибальт: Как ты посмел явиться сюда, чтобы глумиться над нашим праздником? #З
Тибальт: Как смеешь ты подходить к моей кузине Джульетте! #З

Ближайшие к ним гости зашеплтались, десятки глаз обратились в их сторону.
Тибальт, двоюродный брат Джульетты, с ненавистью смотрел на Ромео, его рука уже легла на эфес шпаги. 
Ромео: Неужто Капулетти! Мой светлый ангел - дочь заклятого врага! Ах, вот какую беду я предвидел во сне... #У
Ромео: Как же мне быть? Тибальт настроен решительно, и я еще никогда не уходил от вызова. Но он брат той, кого я люблю. #У

// Критический выбор

Ромео решил:
// Бросить вызов Тибальту
+ [Снять маску и встретить взгляд Тибальта]
    ~ Passion = Passion + 1
    Страсть +1
    Ромео выпрямил спину и открыл свое лицо. 
    По толпе пронесся испуганный вздох, когда стало ясно, что столкновения не избежать. 
    Взбешенный Тибальт попытался вытащить шпагу, но Ромео успел толкнуть его, и в начавшейся суматохе опрокинул на пол серебряный поднос с кубками.
    Пока всеобщее внимание было приковано к беспорядку, Ромео воспользовался моментом и скрылся в толпе, бросив прощальный взгляд на Джульетту.
    
// Сбежать, чтобы не нарваться
+ [Раствориться в толпе]
    ~ Sense = Sense + 1
    Разум +1
    Не говоря ни слова, Ромео опустил голову и сделал шаг назад, позволив толпе гостей скрыть его.
    Отступая он нежно коснулся руки Джульетты, и она обернулась, продолжая смотреть на него, пока не потеряла из виду.
    Тибальт сделал несколько резких шагов вперед, но поняв, что упустил его, издал разочарованный рык и бросился вон из зала.

// Не усугублять вражду между семьями
+ [Обратиться к хозяину дома]
    ~ Sense = Sense + 1  
    ~ Peace = Peace + 1 
    Разум +1, Мир +1
    Ромео нашел глазами главу семьи, старого Капулетти, и громко обратился прямо к нему.
    Ромео: Милорд Капулетти, я благодарю вас за гостеприимство! Ваш дом прекрасен, как и те, кто в нем живет. 
    Тибальт не ожидал этого хода и замер, не смея напасть на гостя.
    Старый Капулетти с гордостью кивнул, бросив на племянника недовольный взгляд.
    Гости зашептались, а Ромео воспользовался замешательством, чтобы проститься с Джульеттой.
    Ромео: Я должен идти, чтобы не злить более вашего брата. Но знайте, мой ангел, вы всего дороже для меня!
    
- Когда Ромео скрылся, Джульетта подбежала к кормилице.
Джульетта: Кто этот юноша? #У
Кормилица: Дитя мое, его зовут Ромео, сын Монтекки.
Джульетта: Неужели правда! #У
Джульетта: Сколь разумнее было бы никогда его не встречать и не влюбляться. #П
Джульетта: Но разве я могу приказывать своему сердцу, когда оно так волнуется при мысли о нем? #У
Кормилица: Вам не стоит видеться. Если он будет разумен, то не будет нарушать твой покой. А скоро родители выберут тебе достойного мужа.
Кормилица: Идем, Джульетта!
Позволяя кормилице увести себя, Джульетта продолжала думать о неожиданной встрече.
-> 2_Balcony

=== 2_Balcony ===

// Встреча Ромео и Джульетты под ее балконом. Признание в любви. Договоренность о свадьбе.
// Звезды на фоне

#Location: Balcony
Сцена 2. У балкона
Покинув бал, Ромео шел сквозь ночную прохладу и не мог выкинуть из головы образ Джульетты. 
Он не хотел допускать мысль, что покинет ее, едва повстречав, и потому он шел в сад Капулетти, надеясь продлить встречу.

Джульетта стояла на балконе, погруженная в свои мысли.

Джульетта: Ромео, ах, зачем же ты Ромео?
Джульетта: Отрекись от отца и своего имени! А если нет, то поклянись мне в любви, и я перестану быть Капулетти.
Ромео: Ловлю тебя на слове! Назови меня своей любовью, и с этого мига я больше не Ромео.
Джульетта: Ах! Как ты сюда пробрался? Стены сада высоки и неприступны. Если мои родные найдут тебя здесь — тебе верная смерть! #У
Ромео: Любовь принесла меня на своих крыльях, и каменные стены ей не преграда. #Р
Джульетта: Но их ненависть найдет тебя... #П
Ромео: Тьма ночи скроет меня от их глаз. Лишь бы ты смотрела на меня с нежностью. #Р

 Ромео подошел ближе к балкону, и Джульетта задумалась, 
 Союз с Ромео был опасен, но она уже не хотела представлять свою жизнь без него.
 Джульетта решила:

// Критический выбор
+ [Довериться сердцу и признаться в любви]
    ~ Passion = Passion + 1
    Страсть +1
    #Location: Balcony_kiss
    Джульетта: К чему сомнения, если я уже знаю ответ в своем сердце? 
    Джульетта: Любовь к тебе как вспышка молнии, которая озарила мою жизнь. Ромео, любишь ли ты меня?
    Ромео: Душа моя, моя любовь к тебе - единственный свет в моей жизни. До встречи с тобой я блуждал во тьме. 
    Ромео: Ты — само Солнце, вокруг которого теперь вращается моя вселенная, и без тепла которого я погибну.
    Ромео взбирается на балкон и припадает к рукам Джульетты.
    Джульетта: Возьми мою любовь в обмен на твою. Я отдаю тебе свое сердце, свою судьбу, все, что у меня есть!
    // Картинка, как Ромео целует руку Джульетты

+ [Проверить серьезность намерений Ромео]
    ~ Sense = Sense + 1
    Разум +1
    #Location: Under_balcony
    Джульетта: Этот союз так внезапен, так похож на вспышку молнии, что гаснет, не успев осветить тьму.
    Джульетта: Я боюсь, что наша радость так же быстро исчезнет.
    Ромео: Клянусь священным ликом луны, что серебрит верхушки этих деревьев...
    Джульетта: О, не клянись луной, изменчивой луной! Твоя любовь может оказаться такой же непостоянной.
    Джульетта: Ромео, если твои намерения честны, а цель твоя — брак, то завтра я пришлю к тебе своего вестника.
    Джульетта: Передай с ним, где и когда ты хочешь совершить обряд.
    // Картинка Ромео под балконом
    
+ [Поделиться с Ромео переживаниями об их семьях]
    ~ Sense = Sense + 1  
    ~ Peace = Peace + 1 
    Разум +1, Мир +1
    #Location: Under_balcony
    Джульетта: Ромео, наша любовь — хрупкий цветок, выросший на каменистой почве ненависти. Мы не сможем быть счастливы, пока наши семьи ненавидят друг друга.
    Ромео: Ты права, мой ангел! От этого она еще более драгоценна. Мы обязательно найдем способ остановить их вражду!
    Джульетта: Боюсь, нам не справиться вдвоем. Нам нужен мудрый советник, который проведет и укажет дорогу.
    Ромео: Отец Лоренцо! Он свят и видит дальше, чем мы. Встретимся у него завтра!
    Джульетта: Да, Ромео! Иди к нему на рассвете. А я, как только будет возможно, пришлю к тебе верного человека.
    // Картинка Ромео под балконом
    
- Ромео: Завтра же я договорюсь об обряде венчания! Я не хочу более и дня жить без тебя!
Джульетта: Тогда лети! Скорее бы прошла ночь, приближая этот сладостный день!
    
-> 3_Wedding

=== 3_Wedding ===
// Влюбленные убеждают отца Лоренцо тайно поженить их, а он проводит им проверку.

#Location: Lorenco
Сцена 3. В келье
Монах стоял у икон и молился. Свет Луны пробивался через узкое окно кельи, освещая пучки сушеных трав и старинные книги. 
Ромео: Джульетта!
Джульетта: Ромео!

Отец Лоренцо: Дети мои. Вы пришли ко мне, ища таинства, которое свяжет вас навек. 
Отец Лоренцо: Но любовь, рожденная в спешке, часто приводит к горькому финалу. 
Отец Лоренцо: Я служу Господу, а не слепой страсти. Прежде чем я соглашусь стать соучастником этого рискованного союза, вы должны ответить на мои вопросы.
Отец Лоренцо: Отвечайте честно, ибо вы отвечате не только мне, но и перед Богом.
-> passion_test
== passion_test ==
Отец Лоренцо: Вопрос первый тебе, Джульетта. О вашей любви.
Отец Лоренцо: Любовь, рожденная во взгляде, хрупка. Что доказывает, что ваша связь — не просто мимолетный каприз, а сила, способная сокрушить стены, что воздвигли ваши семьи?
Джульетта: ...
+ [Наша любовь дает нам силы действовать!]
    ~ Passion = Passion + 1
    ~ Sense = Sense + 1
    ~ Lorenco = Lorenco + 1
    Страсть +1, Разум +1
    Джульетта: Любовь уже заставила его рискнуть жизнью, а меня — пойти против воли родителей.
    Джульетта: Наши чувства сильны, но это и наш осознанный выбор. Мы выбираем друг друга вопреки всему.
    Джульетта: Мы понимаем риск, но готовы многим пожертвовать ради того, чтобы быть вместе.
    
+ [Он занимает все мои мысли!]
    ~ Passion = Passion + 1
    Страсть +1
    Джульетта: Я чувствую любовь всем сердцем! Когда я с ним, ничего другого не существует. Наша любовь сожжет любые преграды!
    
+ [Мы просто хотим быть вместе!]
    Джульетта: Разве этого не может быть достаточно? Мы никому не желаем зла.
 
- Лоренцо выслушал Джульетту, не изменившись в лице, и задал следующий вопрос Ромео.
   -> sense_test
== sense_test ==
Отец Лоренцо: Второй вопрос о вашем будущем. Пламя влюбленности согревает, но со временем оно утихает, оставляя угли. 
Отец Лоренцо: Жизнь — это не только прогулки под луной, но и совместные будни, полные забот. Что вы будете делать, когда восторг пройдет и останется лишь привычка?
Ромео: ...
+ [Когда восторг пройдет, начнется настоящая работа.]
    ~ Sense = Sense + 1
    ~ Passion = Passion + 1
    ~ Lorenco = Lorenco + 1
    Разум +1, Страсть +1
    Ромео: Мы будем учиться быть друзьями, доверять друг другу, прощать слабости.
    Ромео: Угли, о которых вы говорите, отец, могут согревать гораздо дольше, чем пламя. 
    Ромео: Наша цель - построить очаг, а не разжечь костер.
    
+ [Брак - это обещание, и мы будем ему верны.]
    ~ Sense = Sense + 1
    Разум +1
    Ромео: У нас будет общее хозяйство, которое нужно вести, и общие цели.
    Ромео: Привычка, о которой вы говорите, — это не угасание, а укрепление союза.
    Ромео: Наша связь станет прочной, как у всех добрых супругов, которые чтут свой долг.
    
+ [Наша любовь не может угаснуть, святой отец!]
    Ромео: Это пламя, что касается небес! Привычка — это удел других, а не нас. 
    Ромео: Мы будем смотреть друг на друга с тем же восторгом до последнего вздоха!
    
- Лоренцо выслушал Ромео, по-прежнему не меняя выражения лица.
-> peace_test
== peace_test ==
Отец Лоренцо: Вопрос третий. О вашей цели. Этот брак в глазах Господа может стать лекарством для ран Вероны. 
Отец Лоренцо: Но в глазах людей он — искра в пороховой бочке. Как вы объявите семьям о вашем браке?

+ [Нам нет дела до их войны.]
    Ромео: Мой дом теперь - это Джульетта. Мы будем вместе, куда бы судьба нас ни завела.
    Джульетта: Даже если нам придется покинуть наши семьи, я готова прожить свою жизнь с Ромео.
+ [Как только они увидят нашу любовь, они помирятся!]
    Джульетта: Их сердца растают, и они не смогут больше враждовать!
    Ромое: Я не подниму меча против семьи Джульетты и, уверен, они поступят соответственно.
+ [Мы готовы стать первой нитью, сшивающей рану между нашими домами.]
    ~ Peace = Peace + 1 
    ~ Lorenco = Lorenco + 1 
    Мир +1    
    Ромео: Вражда длится так давно, что никто уже и не помнит, с чего она началась. Настало время ее остановить.
    Джульетта: Нам придется проявить терпение и смелость. Но мы готовы попытаться, ведь мы являемся продолжением наших семей.
    
- Отец Лоренцо кивнул и приготовился дать ответ.
-> final_test
== final_test ==
// Поженятся ли герои
{ Lorenco == 0:
    // Лоренцо не убежден и отказывается заключать брак.
    - Отец Лоренцо: Простите, дети мои, но я не стану орудием вашей погибели. 
      Отец Лоренцо:  Ваши ответы показали мне лишь слепую страсть и детские мечты. Ваш союз не принесет мира, он разожжет еще больший пожар. 
      Отец Лоренцо: Вы не готовы. Возвращайтесь в свои дома и молитесь о разуме, а не о любви. В браке вам отказано.
      Джульетта: Я боялась, что вы скажете именно так, святой отец... #П
      Ромео: Не бойся! Я по-прежнему верен своему слову!
      Ромео: Наша любовь найдет способ прорасти, и мы соединим себя узами брака, когда будем готовы. #Р
      Ромео: Мы должен идти, но время разлуки я буду мерить не ходом Солнца, а мыслями о тебе. И знай, что этих мыслей будет так много, что они приблизят нашу следующую встречу.
    -> final_test_end
- else:
    { Lorenco == 3:
        // Лоренцо убежден и заключает брак.
        - Отец Лоренцо: Сам Господь говорит вашими устами. В ваших ответах я вижу не только любовь, но и мудрость, и смирение.
        Отец Лоренцо: Этот союз благословлен. Пойдемте, дети мои. Я обвенчаю вас.
        ~ Juliette = "Romeo"
        ~ Sense = Sense + 1
        ~ Passion = Passion + 1
        ~ Peace = Peace + 1
        Страсть +1, Разум +1, Мир +1
        -> wedding
    - else:
        // Лоренцо сомневается и уточянет, намерены ли герои заключить брак.
        Отец Лоренцо: В ваших словах есть и правда, и опасная наивность. Ваше желание искренне, но фундамент вашего будущего шаток. 
        Отец Лоренцо: Я вижу любовь, но не вижу полной готовности к последствиям.
        Отец Лоренцо: Скажите мне честно, осознавая все риски, вы по-прежнему просите меня обвенчать вас?
        
        * [Заключить брак]
            ~ Juliette = "Romeo"
            ~ Sense = Sense + 1
            ~ Passion = Passion + 1
            Разум +1
            Страсть +1
            Ромео: Да, мы готовы ко всему!
            Джульетта: Лишь ему я готова вручить свою судьбу!
            Отец Лоренцо: Да будет так. И да поможет нам всем Господь.
            -> wedding
        * [Отложить брак]
            Джульетта: Вы правы, отец Лоренцо, нам нужно еще подумать.
            Ромео: Наша любовь найдет способ прорасти, и мы соединим себя узами брака, когда будем готовы.
            Ромео: Мы должны идти, но время разлуки я буду мерить не ходом Солнца, а мыслями о тебе. И знай, что этих мыслей будет так много, что они приблизят нашу следующую встречу.
            -> final_test_end
    }    
}
== wedding ==
// Картинка свадьбы
#Location: Wedding
Клятвы Ромео и Джульетты были не громкими речами для гостей, а тихим шепотом, предназначенным только друг для друга и для Бога. 
Он взял ее руку, холодную от волнения, и почувствовал, как ее пальцы крепко сжали его в ответ. 
Отец Лоренцо произнес слова, которые превратили двух врагов по рождению в единое целое по воле небес. 
-> final_test_end

== final_test_end ==
// В итоге влюбленные расходятся по домам.
Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}


-> 4_Mercucio_death

=== 4_Mercucio_death ===
// Ромео возвращается от отца Лоренцо и видит своих друзей Меркуцио и Бенволио, которые встретилсь с Тибальтом. Конфликт переходит в кровавую фазу.

#Location: Square
Сцена 4. Роковая встреча

Ромео шел по улицам Вероны, и его сердце наполняла тайная радость, которая делала весь мир новым и ярким.
На ценральной площади он увидел своих друзей, Меркуцио и Бенволио, которые столкнулись с Тибальтом.

Меркуцио: Ромео? Вы ищете синьора Ромео? 
Меркуцио: Боюсь, его расписание на сегодня уже заполнено. Но я могу записать вас в лист ожидания. Правда, он довольно длинный, и я слышал, синьор Ромео не принимает тех, кто дурно пахнет злобой.
Бенволио: Меркуцио, прошу тебя, уйдем. В такой день кровь кипит сама по себе, и драки не миновать.

Тибальт: ... #З
Тибальт: Монтекки! Вот и ты! #У
Тибальт: Ты — подлец, что посмел явиться в мой дом прошлой ночью. Ты должен поплатиться за это, доставай свою шпагу. #З 

Ромео: Тибальт, есть причина, по которой я должен любить тебя. Она озволяет мне простить твою ярость и  оскорбление.
Ромео: Я не подлец. А потому прощай, я вижу, ты не знаешь, кто я.

Меркуцио: Что за позорное, трусливое смирение! #У
Меркуцио: Честь требует отмщения! Тибальт, крысиный царь, дерись со мной! #З

Тибальт: Ну что ж, Меркуцио. Раз ты так рвешься в бой, я к твоим услугам. #З

Шпаги со звоном скрестились. 
Ромео в ужасе бросился между ними фетхующими.
Пытаясь их разнять, Ромео встал так, что на мгновение закрыл Меркуцио обзор.
Этого хватило, чтобы Тибальт нанес смертельный удар под рукой Ромео.

// Слова Меркуцио перед смертью
Меркуцио упал на руки Ромео.
Меркуцио: Я сделал это ради тебя, понимаешь? #П
Меркуцио: Я не мог просто стоять и слушать, как он оскорбляет тебя, а ты молчишь. Кто-то должен был защитить твою честь, раз уж ты сам не стал. #З
Меркуцио: Но теперь... теперь из-за этой глупой, бесконечной вражды я умираю. #П
Меркуцио: Чума... чума на оба ваших дома! #П
Меркуцио затих навсегда.

Счастье, переполнявшее Ромео всего минуту назад, сменилось ледяным горем и обжигающей яростью.
Кровь его лучшего друга взывала к отмщению. 
{ Juliette == "Romeo":
    - Но клятва, данная Джульетте, вызывала к миролюбию.
- else:
    Но чувства к Джульеиие взывали к миролюбию.
}

// Критический выбор
Ромео решил:
// Классический путь, ведущий к изгнанию.
+ [Отомстить немедленно, атаковав Тибальта] 
    ~ Tibalt = "Dead"
    ~ Romeo = "Exit"
    ~ Passion = Passion + 1 
    Страсть +1
    Ярость затмила разум Ромео. С громким криком он вытащил шпагу и на нес удар.
    Бой был коротким и жестоким, движимый горем, Ромео оказался быстрее.
    Тибальт упал замертво к его ногам.
    Ромео замер, глядя на то, что наделал. Шпага выпала из его ослабевших пальцев. Огонь ярости, что кипел в нем, погас, оставив после себя лишь холодный ужас. 
    Не просто враг... двоюродный брат Джульетты. Ее кровь.
    Он отомстил за Меркуцио, но какой ценой? Одним ударом он уничтожил их будущее. 
    Как она теперь посмотрит на него? Он — убийца ее брата.
    Ромео: О, я игрушка судьбы! #П
    Бенволио: Беги, Ромео! Герцог казнит тебя! #У
    Ноги Ромео словно вросли в раскаленные камни площади. Он смотрел на два тела у своих ног — друга и врага — и не мог сдвинуться с места.
    Бенволио: Ромео, очнись! Слышишь? Городская стража уже близко! Если они найдут тебя здесь, все будет кончено! #У
    Слово "кончено" вырвало Ромео из оцепенения. Он бросил последний, полный отчаяния взгляд на лицо Меркуцио, затем развернулся и побежал. 
    Он бежал, не разбирая дороги, унося с собой не торжество мести, а лишь ледяной ужас и осознание того, что одним ударом клинка он разрушил свой мир и, возможно, потерял Джульетту навсегда.
    -> 5_1_Farewell
    
// Альтернативные пути, доступные по статам.
+ {Juliette == "Romeo" and Sense >=3} [Сдержать гнев и потребовать правосудия у Герцога.]
    ~ Tibalt = "Exit"
    ~ Sense = Sense + 1 
    Разум +1
    Ромео опустил руку, не давая ярости взять верх.
    Ромео: Я не стану убийцей. Но он ответит за это!
    Когда прибыла городская стража, Ромео указал на убийцу.
    Ромео: Вот он! Тибальт из дома Капулетти убил Меркуцио, родича Герцога! Я требую правосудия!
    Стражники, увидев герб Меркуцио, без колебаний окружили Тибальта.
    Тибальт с яростью и презрением посмотрел на стражников, а затем на Ромео.
    Тибальт: Уберите от меня руки! Ты, трус, Монтекки! Прячешься за законом, вместо того чтобы драться как мужчина! Ты за это ответишь! #З
    В этот момент на площадь прибыл сам Герцог Вероны, его лицо было мрачнее тучи.
    Он увидел тело своего родственника, Меркуцио, и его голос загремел над площадью.
    Герцог: Моя собственная кровь пролита на улицах Вероны из-за вашей бессмысленной вражды! 
    Герцог: Тибальт, за убийство Меркуцио я должен был бы приговорить тебя к смерти.
    Герцог: Но поскольку Ромео, друг убитого, не стал вершить самосуд, а воззвал к закону, я тоже проявлю милосердие. 
    Герцог: Я изгоняю тебя из Вероны, Тибальт Капулетти! Если ты когда-либо будешь замечен в стенах этого города, в тот же час ты умрешь!
    Лицо Тибальта исказилось от ярости. Для такого гордого дворянина изгнание было хуже смерти. 
    Он боролся в руках стражи, его глаза горели ненавистью, устремленной на Ромео, пока стража силой уводила его с площади. 
    Ромео смотрел, как уводят Тибальта, и не чувствовал радости. 
    Да, он был жив и свободен. Меркуцио был отомщен. Но какой ценой? 
    Он только что стал причиной изгнания двоюродного брата Джульетты. Как она посмотрит на него теперь? 
    Он избежал гибели на площади, но, возможно, только что навсегда погубил их любовь.
    -> 5_2_Date
    
+ {Juliette == "Romeo" and Peace >=3} [Сдержать гнев и заклинать семьи перестать враждовать.]
    ~ Parents = Parents + 1
    ~ Peace = Peace + 1 
    Мир +1
    По краю площади стали собираться зеваки. Кто с ужасом, кто с ненавистью, кто со слезами смотрели на разыгравшуюся драму.
    Ромео обратился к толпе, где стояли и Монтекки, и Капулетти.
    Ромео: Он кричал: "Чума на оба ваших дома!", и он был прав!
    Ромео: Посмотрите, что наделала наша общая ненависть! Сколько еще должно умереть за гордыню наших отцов?
    Тибальт: Это ты! Это все из-за тебя, Монтекки! #З
    Тибальт: Если бы ты ответил на мой вызов как мужчина, он был бы сейчас жив! #З 
    Тибальт: Твоя трусость, твои лживые речи о мире — вот что заставило его обнажить шпагу! #З
    Ромео: Ты ищешь виноватого, Тибальт, и я не стану тебя за это судить. 
    Ромео: Но моя честь не в том, чтобы проливать кровь, а в том, чтобы остановить ее. 
    Ромео: Не моя трусость убила его и не твой клинок. Его убила наша ненависть.
    Ромео: Мы можем и дальше бросаться обвинениями, пока не останется никого, кто мог бы их услышать. 
    Ромео: Или мы можем остановиться. Здесь. Сейчас.
    В толпе воцарилось потрясенное молчание. Семена мира были посеяны ценой жизни Меркуцио.
    Бенволио: Городская стража уже близко! #У
    Тибальт посмотрел на безжизненное тело Меркуцио, затем снова на Ромео, и в его взгляде промелькнуло отчаяние.
    Развернувшись, он скрылся в лабиринте веронских улиц.
    -> 5_2_Date

=== 5_1_Farewell ===
// Ромео изгнан
#Location: Balcony
Сцена 5. На закате
Закат окрасил небо Вероны в тревожные, кровавые тона. 
Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. 

{ Juliette == "Romeo":
    - Он взобрался наверх.
    Джульетта: О, Ромео! Это правда? Тибальт... он мертв от твоей руки? #П
    Джульетта: Как мог мой дорогой муж стать убийцей моего брата? #П
    Ромео: Он убил Меркуцио, Джульетта. Моего лучшего друга. #П
    Ромео: Я не владел собой, я был ослеплен горем! Я бы отдал все, чтобы этого не случилось. #П
    Джульетта: Мое сердце разбито на две части. Одна оплакивает брата, другая — страдает за мужа. #П
    Джульетта: Но ты — мой муж. И моя верность — с тобой, до самого конца.
    -> early_bird
- else:
    Джульетта: Как ты мог? #П
    Джульетта: Тибальт... он был моей семьей! #П
    Джульетта: Я смотрела на тебя и видела ангела, но неужели за этим лицом скрывался тот, кто способен на убийство? #П
    Ромео: Он убил Меркуцио, Джульетта. Моего лучшего друга. #П
    Ромео: Я не владел собой, я был ослеплен горем! Я бы отдал все, чтобы этого не случилось. #П
    Джульетта: Я должна ненавидеть тебя... Весь мой род требует этого. #П
    Джульетта: Но мое глупое сердце... оно не может. Оно все еще любит тебя. #У
    -> early_bird
}
== early_bird ==
Гнев и обида утихли, оставив после себя лишь огромную, общую на двоих печаль. 
Они знали, что это их последняя встреча. На рассвете Ромео должен был навсегда покинуть Верону, спасаясь от гнева Герцога. 
Каждая секунда вместе была украденным у судьбы сокровищем, и оба понимали, что оно утекает сквозь пальцы.

// Критический выбор
Джульетта умоляла Ромео:
* [Остаться еще немного, чтобы продлить мгновение]
    ~ Passion = Passion + 1
    Страсть +1
    Джульетта: Нет... не уходи. Останься еще на час, на мгновение! Я не готова прощаться! #У
    Ромео: Любовь моя, каждый миг здесь приближает ко мне смерть. Но ради еще одной минуты с тобой я готов рискнуть вечностью.
    Джульетта: Нет, Ромео! Не рискуй ради меня! Моя любовь к тебе сильнее желания удержать тебя здесь. #П
* [Бежать немедленно ради его безопасности]
    ~ Sense = Sense + 1
    Разум +1
    Джульетта: Ты должен уйти. Сейчас же. 
    Джульетта: Моя любовь к тебе сильнее желания удержать тебя здесь. Я лучше буду жить без тебя, зная, что ты в безопасности, чем потеряю тебя навсегда из-за своей слабости. #П
    Джульетта: Беги, Ромео!
    
- Ромео: Прощай, любовь моя. Живи и верь в нашу скорую встречу.
Он спрыгнул с балкона м исчез в утреннем тумане, оставив Джульетту одну, с разбитым сердцем и ужасающей пустотой впереди.

Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}

-> 6_Ultimatum

=== 5_2_Date ===
// Ромео НЕ изгнан. Вместо прощания — тайная встреча. 
#Location: Balcony
Сцена 5. На закате
Закат окрасил небо Вероны в тревожные, кровавые тона. 

{ Tibalt == "Exit":
    // Тибальт изгнан Герцогом
    - Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. Он взобрался наверх.
      Джульетта: Ромео! Ты жив, ты здесь! Я так боялась... #У
      Джульетта: Но... это правда? Тибальт... мой двоюродный брат... он изгнан из-за тебя? #П
      Ромео: Я не хотел этого, любовь моя, клянусь. #П
      Ромео: Но он убил Меркуцио. Я выбрал закон, а не месть, чтобы не запятнать нашу любовь его кровью. #П
    -> we_choose
- else:
    // Тибальт ушел с площади
    Рискуя всем, Ромео снова пришел к балкону Джульетты, где она уже ждала его, бледная и напуганная. Он взобрался наверх.
    Джульетта: Они говорят, ты стоял над телом Меркуцио и говорил о мире... 
    Джульетта: Ты был так смел, так благороден! Но Тибальт... 
    Джульетта: Я видела его глаза, когда он заходил сегодня. Он этого не простит. Теперь его ненависть будет личной. #П
    Ромео: Я знаю. Я видел это в его взгляде. Поэтому я здесь.
    -> we_choose
}
== we_choose ==
Ромео: Ненависть повсюду. На улицах, в наших домах. #П
Ромео: Единственный островок мира во всей Вероне — это мы с тобой, здесь, сейчас.
Джульетта: Но как долго мы сможем скрываться? Ждать, пока Тибальт вернется, чтобы отомстить? Или пока наши отцы решат нашу судьбу за нас? #У

// Критический выбор
* {Passion >= 4} [Решиться на побег]
    ~ Exit = true
    ~ Passion = Passion + 1
    -> 6_FINAL_Fly_away
    
* {Juliette == "Romeo"} [Решиться объявить о бракосочетании]
    ~ Tell = true
    ~ Passion = Passion + 1
    ~ Sense = Sense + 1
    ~ Peace = Peace + 1
    Разум +1
    Страсть +1
    Мир +1
    Джульетта: Наша любовь чиста, и наш брак благословлен Богом.
    Ромео: Что ты предлагаешь?
    Джульетта: Завтра же мы расскажем все нашим родителям. Они не смогут разорвать союз, скрепленный на небесах.
    
* [Ничего не предпринимать]
    Джульетта: Все произошло так быстро. Голова идет кругом. 
    Джульетта: Давай подождем. Просто один день.
    Ромео: Один день может стоить нам всего, любовь моя.
    Джульетта: Я знаю. Но принять решение в таком смятении — еще опаснее. Дай мне время до завтра.

- Тьма окончательно сгустилась над Вероной.
Ромео: Мне пора уходить. Оставаться здесь — безумие. #У
Джульетта: Будь осторожен, любимый. Теперь каждый уголок этого города — ловушка для тебя.
Он поцеловал ее руки и скрылся в ночи, оставив Джульетту наедине с их общим, таким непростым решением.

Ваши показатели к настоящему моменту: Страсть = {Passion}, Разум = {Sense}, Мир = {Peace}

-> 6_Ultimatum

=== 6_Ultimatum ===
#Location: Capuletti
// На следующей день происходит разговор с родителями.
На следующее утро в комнату Джульетты вошли родители.
-> Tibalt_conv
== Tibalt_conv ==
{ Tibalt == "Exit":
    // Родители сокрушаются об изгнании Тибальта.
    Леди Капулетти: Ох, мой несчастный племянник, моя кровь, изгнан из-за этого проклятого Монтекки. #П
    Лорд Капулетти: Он сам навлек на себя беду своей горячностью. Но позор лег на весь наш дом.
    Джульетта: Тибальт был мне братом, и мое сердце разрывается при мысли о его изгнании. #П
    Лорд Капулетти: Довольно слез, дитя мое! Я принес тебе лекарство от печали, чудесную новость, что высушит твои слезы!
    Джульетта: Какую новость, отец?
    Лорд Капулетти: Достойнейший жених, молодой граф Парис, родственник Герцога, просит твоей руки. Я дал свое согласие! В четверг вы обвенчаетесь!
    Лорд Капулетти: Эта свадьба укрепит положение рода Капулетти и вернет праздник в наш дом!
    -> Juliette_conv
- else:
    { Tibalt == "Dead": 
        // Родители сокрушаются о смерти Тибальта.
        Леди Капулетти: Мой мальчик... Мой Тибальт... Убит на площади, как собака! #П
        Леди Капулетти: Я не успокоюсь, пока убийца не заплатит своей жизнью! #З
        Джульетта: ... #П
        Джульетта: Тибальт был мне братом, и мое сердце разрывается при мысли о том, что его больше нет. #П
        Лорд Капулетти: Довольно слез, дитя мое! Я принес тебе лекарство от печали, чудесную новость, что высушит твои слезы!
        Джульетта: Какую новость, отец?
        Лорд Капулетти: Достойнейший жених, молодой граф Парис, родственник Герцога, просит твоей руки. Я дал свое согласие! В четверг вы обвенчаетесь!
        Лорд Капулетти: Эта свадьба укрепит положение рода Капулетти и вернет праздник в наш дом!
        -> Juliette_conv
    - else: 
        Леди Капулетти: Дочь, мы пришли поговорить о твоем будущем
        {Parents == 1: 
            // Родители упоминают речь Ромео на площади.
            Лорд Капулетти: Я все думаю о том, что произошло на площади. О речи этого мальчишки Монтекки.
            Леди Капулетти: Не говори мне о нем...
            Лорд Капулетти: Нет, ты послушай. Он стоял над телом своего друга и не кричал о мести. 
            Лорд Капулетти: Он говорил о "чуме на оба наших дома". Он обвинил не Тибальта, а нашу "общую ненависть". И толпа... толпа слушала его.
            Джульетта: ...
            Лорд Капулетти: Я устал от этой вражды, жена, клянусь всеми святыми. Она отнимает у нас лучших людей и не приносит ничего, кроме горя. 
            Лорд Капулетти: Но то, что сделал этот мальчишка, — это не призыв к миру. Он выставил Монтекки благородными страдальцами, а нас, Капулетти, — кровожадными убийцами в глазах всего города и самого Герцога!
            Лорд Капулетти: Они сделали свой ход. Теперь наша очередь. И наш ответ должен быть таким, чтобы укрепить наш дом раз и навсегда. Нам нужен могущественный союзник.
            Лорд Капулетти: Граф Парис, родич Герцога, просил твоей руки. Я думал дать тебе время, но теперь времени нет. 
            Лорд Капулетти: Свадьба с ним укрепит наше положение, закроет рты всем нашим врагам и покажет Монтекки, что мы сильны как никогда.
        - else: 
            Лорд Капулетти: Достойнейший жених, молодой граф Парис, родственник Герцога, просит твоей руки. Я дал свое согласие! В четверг вы обвенчаетесь!
            Лорд Капулетти: Эта свадьба укрепит положение рода Капулетти и вернет праздник в наш дом!
            -> Juliette_conv
        }
    }
}
        
        
== Juliette_conv ==
// Родители хотят, чтобы Джульетта вышла замуж за Париса.
Джульетта: ... #П
Джульетта: Отец, я благодарна за вашу заботу, но... я не могу. Это слишком поспешно. #П
Лорд Капулетти: Что значит "не могу"? Я устроил твое счастье! Это мое решение.
Джульетта смотрела на отца с ужасом. 

{ Juliette != "Romeo": 
    Джульетта: Но как же Ромео? Его глаза, его слова, его клятва на балконе... В тот миг я была уверена, что это судьба. Что мы созданы друг для друга.
    { Lorenco == 0: Мы просили благословения у отца Лоренцо, но он отказал нам. Он сказал, что наш союз принесет лишь горе. | Мы должны были обвенчаться. Это должно было сделать нас единым целым, защитить нас перед Богом и людьми. Наша любовь осталась тайной, хрупким сном, который так легко разрушить. }
    Джульетта: Может быть, мне действительно стоит выйти замуж за Париса?
    Джульетта: Он благороден, одобрен семьей. Он предлагает мне спокойную, понятную жизнь без лжи. 
    Джульетта: Я стану графиней, у меня будет дом, положение, уважение. Никто не будет называть меня предательницей.
    Джульетта: Стоит ли бороться за любовь, которая требует столько жертв, но не дает защиты?
}

// Критический выбор
// Джульетта встает перед выбором

* {Juliette != "Romeo"} [Согласиться на брак с Парисом]
    ~ Sense = Sense + 1
    Разум +1
    Бремя тайны и страха оказалось слишком тяжелым для Джульетта. 
    Она сделала выбор, который разобьет ей сердце, но, возможно, спасет от еще большей беды.
    Джульетта: Отец... матушка... вы правы. Я была ослеплена горем и говорила глупости. Я принимаю вашу волю. Я выйду замуж за графа Париса.
    Лорд Капулетти: Что? Вот это моя дочь! Разумная, послушная! Ты сделала меня самым счастливым отцом в Вероне! Ты увидишь, это правильный выбор! #Р
    Леди Капулетти: О, дитя мое, слава святым! Ты не пожалеешь. Граф Парис — прекрасная партия, он сделает тебя счастливой. #Р
    Лорд Капулетти: Мы устроим такой пир, какого Верона не видела! Мы покажем этим Монтекки, что значит настоящий праздник! #Р
    -> 7_FINAL_Wedding_Paris_YES

* {Tell == true} [Признаться, что обвенчалась с Ромео]
    ~ Passion = Passion + 1
    ~ Sense = Sense + 1
    ~ Peace = Peace + 1
    Страсть +1, Разум +1, Мир +1
    Джульетта: (Правда — мой единственный щит. Он разгневается, но он не сможет заставить меня нарушить клятву, данную перед Богом.)
    Джульетта: Отец, я не могу выйти за Париса... потому что я уже замужем. Мой муж — Ромео Монтекки.
    -> told_about_Romeo

* [Категорически отказаться выходить за Париса]
    ~ Passion = Passion + 1
    Джульетта: (Я не могу сказать ему правду, это погубит Ромео. Но и согласиться я не могу. Я должна просто сказать «нет» и выдержать бурю.)
    Джульетта: Отец, прошу вас! Я не выйду замуж за Париса! Я скорее умру!
    Лорд Капулетти: Неблагодарная девчонка! Выбирай: либо ты идешь в церковь в четверг, либо я тебя больше не знаю! #З
    Джульетта: Позвольте мне хотя бы исповедоваться перед свадьбой! У отца Лоренцо! #У
    Лорд Капулетти: Хорошо. Иди. Может, святой отец вложит в твою голову хоть каплю разума и благодарности.
    Лорд Капулетти: Но запомни: это ничего не меняет. В четверг — свадьба.
    -> 7_Lorenco_help

* [Тянуть время]
    ~ Sense = Sense + 1
    Джульетта: (Я не хочу вызывать гнев своего отца, но я не могу согласиться. Может быть он согласится дать отсрочку?
    Джульетта: Отец, молю вас, на коленях! Не спешите со свадьбой! Ведь мы совсем не знаем друг друга. #П
    Лорд Капулетти: Никаких отсрочек! У вас будет вся жизнь, чтобы узнать друг друга. В четверг ты будешь у алтаря, или ты больше не переступишь порог этого дома.
    Джульетта: Позвольте мне хотя бы исповедоваться перед свадьбой! У отца Лоренцо! #У
    Лорд Капулетти: Хорошо. Иди. Может, святой отец вложит в твою голову хоть каплю разума и благодарности.
    Лорд Капулетти: Но запомни: это ничего не меняет. В четверг — свадьба.
    -> 7_Lorenco_help
== told_about_Romeo ==
Лорд Капулетти: Что... ты... сказала? #У
{ Peace >= 4: 
    (У вас высокий показатель "Мир", благодаря этому у Джульетты получится убедить Лорда Капулетти.)
    Джульетта: Вы говорили о будущем нашего рода, отец. О том, как укрепить наш дом. Мой брак с Ромео — это и есть наше будущее.
    Лорд Капулетти: Будущее?! С сыном нашего врага?! Это позор!
    Джульетта: Нет! Это конец вражды! Подумайте! Я — ваша единственная наследница. Он — наследник Монтекки. 
    Джульетта: Наш ребенок... ваш внук... он объединит оба дома! Он унаследует все! 
    Джульетта: Не убивайте ваше собственное наследие из-за вражды, которая вас самого тяготит!
    Ее слова заставили Лорда Капулетти замереть. Он посмотрел на жену, ища поддержки в своей ярости, но увидел в ее глазах не гнев, а страх.
    Леди Капулетти: Она права, муж... { Tibalt == "Dead" or Tibalt == "Exit": Я уже потеряла племянника из-за этой войны. Я не хочу потерять и дочь, и будущее нашего рода. | Наша ненависть уже стоила жизни Меркуцио, родичу Герцога. Теперь весь город смотрит на нас, ждет, что мы прольем еще кровь. }
    Лорд Капулетти долго молчал, его лицо было полем битвы. Наконец, он с шумом выдохнул, и его плечи опустились. Он выглядел постаревшим на десять лет.
    Лорд Капулетти: Ты разорвала наш союз с графом Парисом. Ты опозорила меня перед Герцогом. Ты отдала наследницу дома Капулетти нашему злейшему врагу.
    Лорд Капулетти: Я не могу простить тебе, что ты сделала это без моего ведома. Но то, что сделано, сделано. И раз уж ты начала эту игру, мы доведем ее до конца. По-нашему.
    Лорд Капулетти: Жена, позови брата Лоренцо. Немедленно. Он должен будет подтвердить каждое слово перед Герцогом. И пошли весточку старому Монтекки. Передай, что Лорд Капулетти будет говорить с ним.
     -> 7_FINAL_Peace
- else:
    (У вас низкий показатель "Мир", Лорд Капулетти не воспримет доводы Джульетты.)
    Джульетта: Отец, я сделала это ради любви! Я люблю его!
    Лорд Капулетти: Любовь?! Ты опозорила меня! Опозорила имя Капулетти! #З
    Леди Капулетти: Как ты могла, Джульетта? Как ты могла так поступить с нами? #У
    Лорд Капулетти: Я тебе больше не отец. У тебя нет ни имени, ни дома. Ты выбрала своего Монтекки — так иди к нему! Я лишаю тебя всего — наследства, титула, имени! 
    Джульетта: Отец, нет! #П
    Лорд Капулетти: Вон из моего дома! И чтобы я тебя больше никогда не видел! Ты для меня умерла! #З
    Он развернулся и вышел, не оглядываясь, его шаги гулко отдавались в тишине. Джульетта, рыдая, бросилась к матери, пытаясь обнять ее колени.
    Джульетта: Матушка, молю вас! Не оставляйте меня! #П
    Леди Капулетти: Делай, как хочешь, я за тебя больше не отвечаю.
    Она вышла вслед за мужем, оставив Джульетту одну, распростертую на полу. Шок сменился ледяным отчаянием. 
    Они отреклись от нее. Отец, мать... все. Теперь у нее не было никого. Никого, кроме любимого мужа и надежды на помощь отца Лоренцо. К нему она и отправилась.
    -> 7_FINAL_Juliette_Montecchi
}

=== 7_Lorenco_help === 
Сцена 7. Совет Лоренцо
Рискованная авантюра отца Лоренцо со снадобьем, чтобы помочь влюбленным и помочь потом семьям примириться.
// Критический выбор
Джульетта выбирает:
* {Passion >= 4} [Выпить снадобье без колебаний]
    -> 8_FINAL_CLASSIC
* [Не пить снадобье]
    Отправить гонца к Ромео и надеяться, что Ромео скоро появится.
    -> 8_FINAL_Wedding_Paris_UPS


=== 6_FINAL_Fly_away ==
Страсть +1
Ромео: Тогда давай покинем этот мир ненависти! Бежим из Вероны сегодня же ночью! 
Джульетта: Бежать... Да. Да! Какая разница, где жить, если я буду жить с тобой?
Ромео: Мы отправимся в Мантую! Там мы окажемся свободны от наших имен и власти Герцога. Мы скроемся и начнем и новую жизнь. 
Ромео: Я договорюсь обо всем и вернусь через два часа. Не будем медлить!
Джульетта: Два часа... #У
Джульетта: Я соберу все самое ценное, что смогу унести, — это поможет нам в первое время. 
Джульетта: Прошу, будь осторожен, моя любовь. И возвращайся скорее.

Чрезе два часа в тени сада пофыркивали две оседланные лошади. 
По брошенной им веревочной лестнице Джульетта спустилась вниз, в последний раз взглянув на свой дом, который теперь стал для нее чужим.

#Location: The End
Ромео и Джульетта добрались до Мантуи и начали новую жизнь. Они представились как Джованни и Мария Росси, наследники разорившейся из-за пожара семьи из Падуи.
Драгоценностей Джульетты хватило на то, чтобы снять скромное жилье и прожить первый месяц. Не понимая цену денег они быстро потратили их и столкнулись с необходимостью зарабатывать.
Решив никогда не возвращаться в Верону, Ромео устроился учителем по фехтованию у сына богатого ремесленника. Он стал более молчаливым и более ответственным, обеспечивая пропитание своей семьи.
Домашнее хозяйства легло на плечи Джульетты, лишенной кормилицы и слуг. Она научилась готовить из ничего, торговаться на рынке, лечить детей травами. 
Иногда по ночам, глядя на спящую жену, Ромео вспоминал ту девочку на балконе, и его сердце сжималось от нежности и вины за ту жизнь, в которую он ее привел.
Своему сыну Лоренцо, единственному дожившему до взрослого возраста, они так и не рассказали о его настоящем происхождении.
{ Tibalt == "Exit":
    Лишенный дома и чести, Тибальт скитался по Италии, снедаемый лишь одной мыслью — вернуться в Верону и омыть свой позор в крови Ромео Монтекки.
}
Горе семей Монтекки и Капулетти, лишившихся своих наследников, было безгранично. Каждый из них винил другого, что еще больше ожесточило вражду между семьями.
История Ромео и Джульетты превратилась в мрачную легенду, которую рассказывали детям как предостережение, а их имена стали символом вражды, которая так и не нашла своего завершения.
КОНЕЦ
-> END

=== 7_FINAL_Juliette_Montecchi ===
Сцена 7. Новое имя
#Location: Lorenco
Джульетта, закутанная в темный плащ, прибежала в келью брата Лоренцо, словно раненая птица, ищущая укрытия от бури.
Она бросилась к ногам священника, ее лицо было искажено отчаянием.
Джульетта: Отец! Помогите мне! Мои родители... они отреклись от меня! #П
Джульетта: Они выгнали меня из дома. Они хотят, чтобы я вышла за Париса, но я не могу! Я жена Ромео! #П
Отец Лоренцо: Дитя мое... Я боялся этого. Я молился, чтобы их сердца смягчились. #П
Джульетта: Их сердца — камень! Мне некуда идти, я одна во всем мире! #У
Отец Лоренцо: Твой муж жив, и его дом — теперь твой дом. Твоя верность теперь принадлежит ему.
Отец Лоренцо: Я немедленно пошлю за Ромео. Он должен знать, что его жена нуждается в его защите.
Он позвал молодого послушника, прошептал ему на ухо несколько быстрых слов и отправил его в дом Монтекки.
Отец Лоренцо: Лети быстрее ветра, сын мой. Скажи Ромео, что его жена здесь и была изгнана. Скажи ему прийти немедленно, и прийти как муж. #П
Джульетта осталась ждать в тишине кельи, которая стала ее единственным убежищем. Ее судьба теперь была в руках семьи, которую ее учили ненавидеть всю жизнь.

#Location: Montecchi
Ромео привел Джульетту в дом своего отца. Они вошли в большой, холодный зал, стены которого были украшены гербами Монтекки.
Навстречу им вышли Лорд и Леди Монтекки. Увидев Джульетту, они замерли, их лица стали непроницаемыми масками.
Леди Монтекки: Что... она... здесь делает? #У
Ромео: Матушка, отец... это Джульетта. Моя жена. Отец Лоренцо тайно обвенчал нас.

Ромео: Ее родители отреклись от нее из-за нашего брака! Она моя жена, и я не позволю ей остаться на улице! Вы должны ее принять!
Лорд Монтекки: Любовь? Мальчишка, ты привел в наш дом дочь Капулетти в разгар войны! Уйми свои чувства и включи голову.  #У
Леди Монтекки: Она будет шпионить за нами, передавать наши секреты своим родным. Мы не можем ей доверять. #У

{ Sense < 4:
    (У вас низкий показатель "Разум", Лорд Монтекки не поверит, что Джульетта способна идти по пути разума и стать часть их семьи.)
    Джульетта: Я здесь не из-за имени Монтекки! Я здесь из-за Ромео! Мне все равно, кто победит в вашей вражде. 
    Джульетта: Мне важно лишь то, чтобы я могла быть рядом с Ромео. Без него для меня нет ни Вероны, ни мира, ничего!
    Лорд Монтекки: Слова ничего не значат. Ты будешь жить здесь, раз уж Ромео совершил эту глупость. Я не буду лишать наследства своего единственного сына.
    Лорд Монтекки: Но не думай, что мы забудем о том, кто ты такая. За каждым твоим шагом будут следить.
    Ромео: Отец. Вы можете следить за ней, но она — моя жена. Она не гостья в этом доме, она — его будущая хозяйка.
    Лорд Монтекки: Ты такой же слепец, как и она. Но пусть будет так. Может быть, однажды ваш сын сможет поступить более разумно. 
    Ромео: Джульетта, не слушай их. Пока мы вместе, мы не в тюрьме. Мы — дома.
    Джульетта кивнула, но холод стен этого чужого дома, казалось, проникал в самую душу. Она была с ним, но они были одни против всего мира.
    -> disbelief
- else:
    (У вас высокий показатель "Разум", Лорд Монтекки убедится, что Джульетта способна идти по пути разума и стать часть их семьи.)
    Джульетта: Милорд. Вы правы, что не доверяете мне. Я — Капулетти. Я выросла, слушая о вражде наших домов. Если бы я была на вашем месте, я бы думала то же самое.
    Джульетта: Я не буду говорить вам о любви, потому что это чувство принадлежит только мне и вашему сыну. Я скажу о другом. 
    Джульетта: Я пришла сюда не как Капулетти, а как жена Ромео. Мой долг и моя верность теперь принадлежат ему, а значит — дому Монтекки.
    Джульетта: Мой отец отрекся от меня, потому что я выбрала не ненависть, а вашего сына. Я не прошу вас забыть, кто я. 
    Джульетта: Я прошу вас дать мне шанс доказать своими делами, а не словами, что я хочу того же, чего и вы: чтобы дом Ромео был в безопасности, и чтобы эта бессмысленная война наконец закончилась, не отняв у нас еще кого-нибудь.
    Лорд Монтекки: Твои слова разумны. Дела покажут их истинность. Ты останешься здесь, и тебя будут почитать как жену Ромео.
    Лорд Монтекки: Мы дадим тебе наш кров и нашу защиту. А ты, в свою очередь, подаришь этому дому верность... и наследника.
    -> honour
}
== honour ==
#Location: The End
Ромео и Джульетта начали новую, сложную жизнь под одной крышей с Монтекки. 
Джульетта, проявив невероятную мудрость и такт, постепенно завоевала уважение родителей Ромео, став для них не врагом, а частью их семьи.
Она помогала Леди Монтекки управлять домом, а ее знание дел Капулетти дало Лорду Монтекки невиданное преимущество в их вековой борьбе.
Ромео, видя силу и ум своей жены, перестал быть пылким юношей и превратился в достойного наследника своего дома, научившись ценить стратегию выше страсти.
Их любовь, закаленная в политических интригах, стала не огнем, а прочным союзом, основанным на взаимном уважении.
Их первенец, названный в честь Меркуцио, стал символом будущего дома Монтекки, которому суждено было унаследовать ослабленное и лишенное прямого наследника состояние Капулетти.
{ Tibalt == "Exit":
Лишенный дома и чести, Тибальт скитался по Италии. Он отказался от мести, понимая, что проиграл не Ромео, а собственной сестре, которая оказалась умнее и дальновиднее его.
}
Горе семьи Капулетти, потерявшей не только дочь, но и всякую надежду на будущее, было безгранично. Они медленно угасали, наблюдая, как их злейшие враги, укрепившиеся благодаря их собственной дочери, становятся самой могущественной семьей в Вероне.
История Ромео и Джульетты превратилась в поучительную легенду о том, как одна женщина смогла закончить вековую войну, впустив в сердце семью своего мужа.
КОНЕЦ
-> END
== disbelief ==
#Location: The End
Ромео и Джульетта начали свою совместную жизнь в доме Монтекки, но она была больше похожа на заключение. 
Джульетту поселили в лучших комнатах, но за каждым ее шагом следили, не доверяя ни единому ее слову.
Ее страстные и эмоциональные речи лишь укрепляли родителей Ромео во мнении, что она — наивная и опасная девчонка, ведомая лишь капризами.
Ромео, разрываясь между любовью к жене и долгом перед семьей, постепенно отдалялся от родителей. Он защищал Джульетту, но не мог дать ей ни свободы, ни настоящего дома.
Их любовь, лишенная воздуха и доверия, начала угасать под гнетом подозрений.
Иногда по ночам, глядя на спящую жену, Ромео вспоминал ту девочку на балконе и понимал, что, пытаясь спасти ее, он запер ее в еще более страшной тюрьме.
Их сын, которого назвали в честь Меркуцио, стал единственной надеждой и наследником рода Монтекки, но его с детства учили не доверять материнской крови Капулетти.
{ Tibalt == "Exit":
Лишенный дома и чести, Тибальт скитался по Италии. Его ненависть к Ромео лишь усилилась, ведь тот не просто женился на его сестре, но и запер ее в доме врага, как трофей.
}
Горе семьи Капулетти, лишившейся дочери, смешивалось со злорадством от того, что Монтекки сами привели в свой дом змею. Вражда не утихла, а превратилась в холодную, подковерную войну шпионов и интриг.
История Ромео и Джульетты превратилась в печальную сказку о том, как даже самая сильная любовь может быть задушена в стенах дома, где нет доверия.
КОНЕЦ
-> END

=== 7_FINAL_Peace ===
#Location: Duke_Hall
Сцена 7. Суд Герцога
Тронный зал был полон напряженного молчания. 
По одну сторону стоял клан Монтекки, по другую — Капулетти.
Герцог сидел на троне, его лицо было суровым, как у судьи.
Ромео и Джульетта стояли перед ним.

// Прописать
Ромео: 
Джульетта: 

Если Тибальт изгнан, можно попросить его вернуть. Герцог согласится.

// Будут ли тут развилки?

#Location: The End
Ромео и Джульетта стали символами новой Вероны. Их публичная свадьба была грандиозным праздником, на котором впервые за сто лет Монтекки и Капулетти сидели за одним столом. 
Всю дальнейшую жизни они были вынуждены постоянно доказывать свою преданность примирению, жертвуя личным счастьем ради общественного блага. 
Ромео стал дипломатом, а Джульетта — хозяйкой салона, где бывшие враги учились говорить друг с другом. Их пылкая любовь переродилась в зрелое партнерство, полное ответственности и усталости.
Их дети росли, зная, что на их плечах лежит огромный груз — не дать старой ненависти возродиться.
Вражда в Вероне действительно закончилась. Старики медленно прощали, а молодежь росла в городе, где имена Монтекки и Капулетти означали не войну, а союз.
История Ромео и Джульетты превратилась в героическую легенду о том, как любовь двух юных сердец оказалась сильнее вековой ненависти, но заплатила за это своим уединением.
КОНЕЦ
-> END

// Финльный бал! = примирение
// Герцог и бал

== 7_FINAL_Wedding_Paris_YES == 
#Location: Wedding_Paris
Джульетта не была замужем за Ромео, и потому согласилась на брак с Парисом.

{Romeo == "Exit"} -> daleko_drug_ot_druga
{Romeo != "Exit"} -> in_Verona
== daleko_drug_ot_druga ==
    Если Ромео в изгнании, то он прочитает письмо о свадьбе и будет жить свою жизнь в Мантуе.
    #Location: The End   
    Ромео и Джульетта прожили каждый свою жизнь вдали друг от друга.
    Джульетта стала графиней, одной из самых уважаемых и влиятельных дам Вероны. Она была верной женой и любящей матерью своим детям. Ее жизнь была спокойной, богатой и абсолютно безопасной.
    Ромео со временем тоже женился в Мантуе, стал уважаемым горожанином и отцом. Он был хорошим мужем, но его жена чувствовала, что какая-то его часть осталась давным-давно, в другом городе.
    Иногда, в тихие ночи, Ромео и Джульетта оба смотрели на одну и ту же луну и вспоминали о балконе, о тайных клятвах и о той жизни, которая могла бы быть, если бы они были чуть смелее или чуть безумнее.
    Тибальт лежал в родовом склепе, невольно отомщенный. Его смерть привела к изгнанию Ромео и ускорила брак, который навсегда разлучил влюбленных.
    Горе семьи Монтекки было тихим и долгим. Они так и не увидели своего единственного сына снова. Их род в Вероне медленно угасал, лишенный наследника и будущего.
    Семья Капулетти отпраздновала свою победу. Они заключили выгодный союз с родственником Герцога и укрепили свое положение. 
    История Ромео и Джульетты не стала великой трагедией. Она превратилась в тихий, грустный шепот о любви, которая оказалась слишком хрупкой для реального мира.
    КОНЕЦ
    -> END
    
== in_Verona ==
Если Ромео в Вероне, то проверяем, у него пусть страсти или разума.
{Sense >= Passion:
(Ваш показатель "Разума" больше, чем показатель "Страсти", поэтому Ромео решил не идти на свадьбу Париса и Джульетты.)
#Location: The End  
Любовь Ромео и Джульетты, яркая и короткая, как вспышка молнии, осталась лишь тайным воспоминанием, о котором они больше никому не рассказывали.
Ромео, подавив в себе боль, не пошел на свадьбу. Он остался в Вероне и со временем, по воле отца, женился на благородной девице из союзного их дому рода. 
Он стал главой своего рода, мудрым и уважаемым, но его жена  чувствовала, что какая-то его часть осталась давным-давно, в другом городе.
Джульетта стала графиней, верной женой и матерью. Ее жизнь была спокойной, богатой и абсолютно безопасной. 
Иногда, глядя на своих детей, она ловила себя на мысли, что могла бы прожить совсем другую, более опасную, но, возможно, более интересную жизнь.
Иногда, в тихие ночи, Ромео и Джульетта оба смотрели на одну и ту же луну и вспоминали о балконе, о тайных клятвах и о той жизни, которая могла бы быть, если бы они были чуть смелее или чуть безумнее.
    { Tibalt == "Exit": 
    Возможно, будет что-то про Тибальта.
    // ДОПИСАТЬ
    }
Вражда между Монтекки и Капулетти продолжала витать в воздухе, но постепенно превратилась в холодное, презрительное соседство, где каждый из домов считал, что выиграл эту безмолвную войну.
История Ромео и Джульетты не стала легендой. Она просто не случилась, оставшись лишь тайной двух сердец, выбравших долг вместо страсти.
КОНЕЦ
-> END

- else:
(Ваш показатель "Страсти" больше, чем показатель "Разум", поэтому Ромео решил пойти на свадьбу Париса и Джульетты.)
#Location: The End 
Ромео и Джульетта так и не начали свою новую жизнь. Ослепленный отчаянием, Ромео пришел на свадьбу, чтобы в последний раз увидеть ту, кого он потерял.
    { Tibalt == "ok": 
    Для Тибальта это был дар судьбы. Увидев своего врага на пороге церкви, он не стал ждать. 
    На глазах у сотен гостей, включая самого Герцога, он вызвал Ромео на поединок, и на этот раз никто не смог его остановить. 
    Ромео пал от клинка Тибальта.
    Джульетта, ставшая в один миг и женой, и свидетельницей смерти своей первой любви, лишилась чувств. Ее брак с Парисом начался с крови, и ее дальнейшая жизнь стала тихим, безмолвным трауром в золотой клетке.
    Тибальт отомстил за честь своей семьи, но цена была высока. Убив Монтекки на глазах у правителя, он был немедленно схвачен и изгнан из Вероны навсегда. Он стал героем для своего рода, но изгоем для всего мира.
    - else:
        { Tibalt == "Exit": 
            Дорогу Ромео преградил жених — граф Парис. Увидев незваного гостя, врага дома Капулетти, он обнажил шпагу, чтобы защитить честь своей невесты и своего дома. 
            Ромео пал от клинка Париса.
            Джульетта, ставшая в один миг и женой, и свидетельницей смерти своей первой любви, лишилась чувств. 
            Она до конца своих дней жила в роскоши и почете, но ее сердце осталось мертвым в тот день у алтаря. Каждый взгляд на мужа был для нее молчаливым напоминанием о том, кого она потеряла.
            Парис был оправдан Герцогом. Как родственник правителя и оскорбленный жених, он действовал в своем праве, защищая свою свадьбу от врага. 
        }
    }
Горе семьи Монтекки было безграничным и яростным. Их единственного сына убили на глазах у всего города, и они поклялись отомстить, когда снова восстановят свой род.
Семья Капулетти получила свой выгодный союз и одержала победу в этой вражде. 
История Ромео и Джульетты превратилась в жестокое предостережение. Их имена стали символом не любви, а кровавой вражды, которая, получив новую жертву, разгорелась с новой, еще более страшной силой.
КОНЕЦ
}
-> END

== 8_FINAL_Wedding_Paris_UPS ==
На свадьбу Париса и Джульетты приходят отец Лоренцо и Ромео. 
Они заявляют, что свадьба не может состояться.
Герцог вынужден признать сбрак.

Если есть Тибальт, он может признать брак или быть изгнан.
Если Тибальт изгнан, можно попросить его вернуть. Герцог откажется, т.к. он чувствует себя опозоренным по случаю разрушенной свадьбы своего родственника.
КОНЕЦ
-> END

=== 8_FINAL_CLASSIC ===
#Location: Cripta
Джульетта принимает снадобье.
Ромео узнает, что она мертва и приходит к ней тоже умирать.
Утром их находят мертвыми в склепе.

#Location: The End
Ромео и Джульетта так и не начали свою новую жизнь. 
Их любовь, вспыхнувшая как звезда, погасла в фамильном склепе Капулетти, став последней и самой страшной жертвой вековой вражды.
Ромео, поверив в смерть своей возлюбленной, выпил яд, чтобы соединиться с ней в вечности. Джульетта, проснувшись и увидев его бездыханное тело, заколола себя его кинжалом, отказавшись жить в мире, где нет его любви.
Их смерть не была напрасной. Увидев своих детей, лежащих бок о бок, старые Монтекки и Капулетти наконец прозрели.
Горе семей Монтекки и Капулетти, лишившихся своих наследников, было безгранично. Но оно было общим. Перед лицом этой невосполнимой утраты они пожали друг другу руки над телами своих детей.
Вражда, которую не могли остановить ни указы Герцога, ни пролитая кровь, была окончена ценой жизни двух влюбленных.
История Ромео и Джульетты превратилась в самую печальную повесть на свете, которую рассказывали как вечное напоминание о том, какую страшную цену порой приходится платить за мир.
КОНЕЦ
-> END
